;{21X}
;Willow Creek generator start/stop program
;Writen by: Bruce Cook
;Last modified: November 2002
;
*Table 1 Program
  01: 60        Execution Interval (seconds)

1:  If time is (P92) ;  Execute generator start every 8 hours, at 50 minutes into hour
 1: 50       Minutes into a
 2: 480      Minute Interval
 3: 30       Then Do

     2:  Z=Z+1 (P32) ; Record number of starts attempted
      1: 3        Z Loc [ attempts  ]

     3:  Beginning of Loop (P87) ; Allow generator 3 attempts to start
      1: 1        Delay
      2: 3        Loop Count

          4:  Set Port (P20)
           1: 01       Set High
           2: 1        Port Number

          5:  Excitation with Delay (P22) ; crank for 30 seconds
           1: 1        Ex Channel
           2: 0000     Delay w/Ex (units = 0.01 sec)
           3: 30       Delay After Ex (units = 0.01 sec)
           4: 0.0      mV Excitation

          6:  Batt Voltage (P10) ; Measure battery voltage
           1: 1        Loc [ Battery   ]

          7:  Set Port (P20)
           1: 0        Set Low
           2: 1        Port Number

          8:  If (X<=>F) (P89) ; Set start flag high if generator starts
           1: 1        X Loc [ Battery   ]
           2: 3        >=
           3: 13       F
           4: 11       Set Flag 1 High ;

          9:  If Flag/Port (P91)
           1: 11       Do if Flag 1 is High
           2: 31       Exit Loop if True

     10:  End (P95) ; End restart loop

11:  End (P95) ; If time end

12:  If Flag/Port (P91) ;  Generator has started!
 1: 11       Do if Flag 1 is High
 2: 30       Then Do

     13:  Z=Z+1 (P32) ; Record number of sucessful starts
      1: 4        Z Loc [ starts    ]

     14:  Do (P86) ; Reset start flag
      1: 21       Set Flag 1 Low

     15:  Z=F (P30) ; Reset timer
      1: 0        F ;
      2: 2        Z Loc [ timer     ]

     16:  Do (P86) ; Set count flag high
      1: 12       Set Flag 2 High

17:  End (P95) ; End

18:  If Flag/Port (P91) ; Turn off generator after counting x number of minutes
 1: 12       Do if Flag 2 is High
 2: 30       Then Do

     19:  Z=Z+1 (P32) ; Index timer
      1: 2        Z Loc [ timer     ]

     20:  If (X<=>F) (P89) ; Stop generator after 60 minutes of charging
      1: 2        X Loc [ timer     ]
      2: 1        =
      3: 60       F
      4: 30       Then Do

          21:  Set Port (P20)
           1: 01       Set High
           2: 2        Port Number ;

          22:  Excitation with Delay (P22) ; 0.5 second toggle
           1: 1        Ex Channel
           2: 0        Delay w/Ex (units = 0.01 sec)
           3: 50       Delay After Ex (units = 0.01 sec)
           4: 0        mV Excitation

          23:  Set Port (P20)
           1: 0        Set Low
           2: 2        Port Number

          24:  Do (P86)
           1: 22       Set Flag 2 Low

     25:  End (P95)

26:  End (P95)

*Table 2 Program
  02: 0.0000    Execution Interval (seconds)

*Table 3 Subroutines

End Program

-Input Locations-
1 Battery   1 1 1
2 timer     1 2 2
3 attempts  1 1 1
4 starts    1 1 1
5 _________ 0 0 0
6 _________ 0 0 0
7 _________ 0 0 0
8 _________ 0 0 0
9 _________ 0 0 0
10 _________ 0 0 0
11 _________ 0 0 0
12 _________ 0 0 0
13 _________ 0 0 0
14 _________ 0 0 0
15 _________ 0 0 0
16 _________ 0 0 0
17 _________ 0 0 0
18 _________ 0 0 0
19 _________ 0 0 0
20 _________ 0 0 0
21 _________ 0 0 0
22 _________ 0 0 0
23 _________ 0 0 0
24 _________ 0 0 0
25 _________ 0 0 0
26 _________ 0 0 0
27 _________ 0 0 0
28 _________ 0 0 0
-Program Security-
0
0000
0000
