;{CR10X}
;CO2 PROFILER, SOIL MEASUREMENTS, PAR SENSORS, WIND SPEED/DIRECTION
;Last updated 8 November 1998

*Table 1 Program
  01: 1         Execution Interval (seconds)

;TAKE LICOR READING DURING FINAL 15 SECONDS
1:  Volt (SE) (P1)
 1: 3        Reps
 2: 15       2500 mV Fast Range
 3: 1        SE Channel
 4: 1        Loc [ LICOR_T   ]
 5: 1        Mult
 6: 0.0      Offset ;

LICOR_T=LICOR_T*2*.01
LICOR_P=LICOR_P*2*0.01522+58.738

2:  Running Average (P52)
 1: 3        Reps
 2: 1        First Source Loc [ LICOR_T   ]
 3: 25       First Destination Loc [ Avg_T     ]
 4: 15       Number of Values in Avg Window

;PARs AND ATMOSPHERIC METEOROLOGY
3:  If time is (P92)
 1: 0     -- Minutes (Seconds --) into a
 2: 5        Interval (same units as above)
 3: 30       Then Do

4:  Volt (Diff) (P2) ; ASCENSION PAR
 1: 1        Reps
 2: 1        2.5 mV Slow Range
 3: 6        DIFF Channel
 4: 69       Loc [ PAR_In    ]
 5: -2110    Mult
 6: 0.0      Offset

;AM25T MEASUREMENTS
5:  Set Port(s) (P20)
 1: 3399     C8..C5 = 1ms/1ms/nc/nc
 2: 9999     C4..C1 = nc/nc/nc/nc

6:  Do (P86) ; ENABLE AM25T
 1: 47       Set Port 7 High

7:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

8:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

9:  Volt (Diff) (P2) ; LI-COR QUANTUM (PAR) SENSOR
 1: 1        Reps
 2: 3        25 mV Slow Range
 3: 5        DIFF Channel
 4: 70       Loc [ PAR_60ft  ]
 5: 349.3    Mult
 6: 0.0      Offset

10:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

11:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

12:  Volt (Diff) (P2) ; LI-COR QUANTUM (PAR) SENSOR
 1: 1        Reps
 2: 3        25 mV Slow Range
 3: 5        DIFF Channel
 4: 71       Loc [ PAR_40ft  ]
 5: 315.4    Mult
 6: 0.0      Offset

13:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

14:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

15:  Volt (Diff) (P2) ; LI-COR QUANTUM (PAR) SENSOR
 1: 1        Reps
 2: 3        25 mV Slow Range
 3: 5        DIFF Channel
 4: 72       Loc [ PAR_25ft  ]
 5: 377.1    Mult
 6: 0.0      Offset

16:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

17:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

18:  Volt (Diff) (P2) ; LI-COR QUANTUM (PAR) SENSOR
 1: 1        Reps
 2: 3        25 mV Slow Range
 3: 5        DIFF Channel
 4: 73       Loc [ PAR_2m    ]
 5: 331.8    Mult
 6: 0.0      Offset

19:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

20:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

21:  Volt (Diff) (P2) ; REBS NET RADIOMETER (2 m)
 1: 1        Reps
 2: 4        250 mV Slow Range
 3: 5        DIFF Channel
 4: 90       Loc [ NetRad_2m ]
 5: 1.0      Mult
 6: 0.0      Offset

22:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

23:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

24:  Volt (Diff) (P2) ; RM YOUNG 03002 WIND SENTRY (WIND SPEED)
 1: 1        Reps
 2: 4        250 mV Slow Range
 3: 5        DIFF Channel
 4: 74       Loc [ WSpd_80ft ]
 5: .045     Mult
 6: .2       Offset

25:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

26:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

27:  Volt (Diff) (P2) ; RM YOUNG 03002 WIND SENTRY (WIND SPEED)
 1: 1        Reps
 2: 4        250 mV Slow Range
 3: 5        DIFF Channel
 4: 75       Loc [ WSpd_40ft ]
 5: .045     Mult
 6: .2       Offset

28:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

29:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

30:  Volt (Diff) (P2) ; RM YOUNG 03002 WIND SENTRY (WIND SPEED)
 1: 1        Reps
 2: 4        250 mV Slow Range
 3: 5        DIFF Channel
 4: 76       Loc [ WSpd_2m   ]
 5: .045     Mult
 6: .2       Offset

31:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

32:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

33:  Excite-Delay (SE) (P4) ; RM YOUNG 03002 WIND SENTRY (WIND DIRECTION)
 1: 1        Reps
 2: 5        2500 mV Slow Range
 3: 8        SE Channel
 4: 2        Excite all reps w/Exchan 2
 5: 2        Delay (units 0.01 sec)
 6: 2500     mV Excitation
 7: 77       Loc [ WDir_80ft ]
 8: .142     Mult
 9: 0.0      Offset

34:  Excite-Delay (SE) (P4) ; RM YOUNG 03002 WIND SENTRY (WIND DIRECTION)
 1: 1        Reps
 2: 5        2500 mV Slow Range
 3: 9        SE Channel
 4: 2        Excite all reps w/Exchan 2
 5: 2        Delay (units 0.01 sec)
 6: 2500     mV Excitation
 7: 78       Loc [ WDir_40ft ]
 8: .142     Mult
 9: 0.0      Offset

35:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

36:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

37:  Excite-Delay (SE) (P4) ; RM YOUNG 03002 WIND SENTRY (WIND DIRECTION)
 1: 1        Reps
 2: 5        2500 mV Slow Range
 3: 8        SE Channel
 4: 2        Excite all reps w/Exchan 2
 5: 2        Delay (units 0.01 sec)
 6: 2500     mV Excitation
 7: 79       Loc [ WDir_2m   ]
 8: .142     Mult
 9: 0.0      Offset

38:  Do (P86) ; ENABLE CS500 T/RH PROBES
 1: 41       Set Port 1 High

39:  Excitation with Delay (P22)
 1: 1        Ex Channel
 2: 0000     Delay W/Ex (units = 0.01 sec)
 3: 10       Delay After Ex (units = 0.01 sec)
 4: 0000     mV Excitation

40:  Volt (SE) (P1) ; CS500 AIR T/RH PROBE (TEMP)
 1: 1        Reps
 2: 5        2500 mV Slow Range
 3: 9        SE Channel
 4: 80       Loc [ AirT_80ft ]
 5: .1       Mult
 6: -40      Offset

41:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

42:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

43:  Volt (SE) (P1) ; CS500 AIR T/RH PROBE (TEMP)
 1: 2        Reps
 2: 5        2500 mV Slow Range
 3: 8        SE Channel
 4: 81       Loc [ AirT_60ft ]
 5: .1       Mult
 6: -40      Offset

44:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

45:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

46:  Volt (SE) (P1) ; CS500 AIR T/RH PROBE (TEMP)
 1: 2        Reps
 2: 5        2500 mV Slow Range
 3: 8        SE Channel
 4: 83       Loc [ AirT_25ft ]
 5: .1       Mult
 6: -40      Offset

47:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

48:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

49:  Volt (SE) (P1) ; CS500 AIR T/RH PROBE (RH)
 1: 2        Reps
 2: 5        2500 mV Slow Range
 3: 8        SE Channel
 4: 85       Loc [ RH_80ft   ]
 5: .1       Mult
 6: 0        Offset

50:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

51:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

52:  Volt (SE) (P1) ; CS500 AIR T/RH PROBE (RH)
 1: 2        Reps
 2: 5        2500 mV Slow Range
 3: 8        SE Channel
 4: 87       Loc [ RH_40ft   ]
 5: .1       Mult
 6: 0        Offset

53:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

54:  Do (P86) ; CLOCK AM25T
 1: 78       Pulse Port 8

55:  Volt (SE) (P1) ; CS500 AIR T/RH PROBE (RH)
 1: 1        Reps
 2: 5        2500 mV Slow Range
 3: 8        SE Channel
 4: 89       Loc [ RH_2m     ]
 5: .1       Mult
 6: 0        Offset

56:  Do (P86)
 1: 51       Set Port 1 Low ; DISABLE CS500 T/RH PROBES

57:  Do (P86)
 1: 57       Set Port 7 Low

58:  Beginning of Loop (P87) ; MINIMUM WIND SPEED LOOP
 1: 0000     Delay
 2: 3        Loop Count

59:  If (X<=>F) (P89) ; MINIMUM WIND SPEED
 1: 74    -- X Loc [ WSpd_80ft ]
 2: 1        =
 3: .2       F
 4: 30       Then Do

60:  Z=F (P30)
 1: 0.0      F
 2: 00       Exponent of 10
 3: 74    -- Z Loc [ WSpd_80ft ]

61:  End (P95) ; END MINIMUM WIND SPEED CORRECTION

62:  End (P95) ; END MINIMUM WIND SPEED LOOP

;RON'S NETRAD CALCULATIONS
63:  If (X<=>F) (P89)
 1: 90       X Loc [ NetRad_2m ]
 2: 3        >=
 3: 0        F
 4: 30       Then Do

64:  Z=X*F (P37)
 1: 76       X Loc [ WSpd_2m   ]
 2: .2       F
 3: 92       Z Loc [ Rn_C      ]

65:  Z=X*F (P37)
 1: 92       X Loc [ Rn_C      ]
 2: .066     F
 3: 93       Z Loc [ Rn_A      ]

66:  Z=X+F (P34)
 1: 92       X Loc [ Rn_C      ]
 2: .066     F
 3: 94       Z Loc [ Rn_B      ]

67:  Z=X/Y (P38)
 1: 93       X Loc [ Rn_A      ]
 2: 94       Y Loc [ Rn_B      ]
 3: 95       Z Loc [ Rn_CrtnFc ]

68:  Z=Z+1 (P32)
 1: 95       Z Loc [ Rn_CrtnFc ]

69:  Z=X*F (P37)
 1: 90       X Loc [ NetRad_2m ]
 2: 9.13     F
 3: 91       Z Loc [ NetRad2   ]

70:  Z=X*Y (P36)
 1: 91       X Loc [ NetRad2   ]
 2: 95       Y Loc [ Rn_CrtnFc ]
 3: 91       Z Loc [ NetRad2   ]

71:  Else (P94)

72:  Z=X*F (P37)
 1: 76       X Loc [ WSpd_2m   ]
 2: .00174   F
 3: 93       Z Loc [ Rn_A      ]

73:  Z=X+F (P34)
 1: 93       X Loc [ Rn_A      ]
 2: .99755   F
 3: 95       Z Loc [ Rn_CrtnFc ]

74:  Z=X*F (P37)
 1: 90       X Loc [ NetRad_2m ]
 2: 11.35    F
 3: 91       Z Loc [ NetRad2   ]

75:  Z=X*Y (P36)
 1: 91       X Loc [ NetRad2   ]
 2: 95       Y Loc [ Rn_CrtnFc ]
 3: 91       Z Loc [ NetRad2   ]

76:  End (P95) ; END RON'S NETRAD CALCULATIONS

77:  End (P95) ; END PAR AND ATMSPHERIC METEOROLOGY MEASUREMENTS

;BATTERY VOLTAGE, SOIL/TREE TEMPERATURE, AND SOIL MOISTURE MEASUREMENTS
78:  If time is (P92)
 1: 0000     Minutes (Seconds --) into a
 2: 10       Interval (same units as above)
 3: 30       Then Do

;BATTERY VOLTAGE
79:  Batt Voltage (P10)
 1: 30       Loc [ Battery   ]

80:  Temp (107) (P11) ; CR10XCR THERMOCOUPLE REFERENCE ON AM416
 1: 1        Reps
 2: 4        SE Channel
 3: 1        Excite all reps w/E1
 4: 31       Loc [ Ref_T     ]
 5: 1.0      Mult
 6: 0.0      Offset ;

81:  Do (P86)
 1: 44       Set Port 4 High ; ENABLE AM416

82:  Do (P86)
 1: 46       Set Port 6 High ; ENABLE CS615s

83:  Beginning of Loop (P87) ; BEGIN THERMOCOUPLE AND TDR MEASUREMENTS
 1: 0000     Delay
 2: 13       Loop Count

84:  Do (P86)
 1: 75       Pulse Port 5 ; PULSE CLOCK

85:  Excitation with Delay (P22)
 1: 1        Ex Channel
 2: 0        Delay W/Ex (units = 0.01 sec)
 3: 10       Delay After Ex (units = 0.01 sec)
 4: 0        mV Excitation

86:  Thermocouple Temp (DIFF) (P14) ; AIR AND SOIL THERMOCOUPLES
 1: 1        Reps
 2: 1        2.5 mV Slow Range
 3: 3        DIFF Channel
 4: 1        Type T (Copper-Constantan)
 5: 31       Ref Temp (Deg. C) Loc [ Ref_T     ]
 6: 32    -- Loc [ AirT_200  ]
 7: 1.0      Mult
 8: 0.0      Offset ;

87:  Period Average (SE) (P27) ; CS615 SOIL WATER CONTENT REFLECTOMETER PROBES
 1: 1        Reps
 2: 4        200 kHz Max Freq @ 2 V Peak to Peak, Period Output
 3: 7        SE Channel
 4: 10       No. of Cycles
 5: 5        Timeout (units = 0.01 seconds)
 6: 48    -- Loc [ TDR1_Per  ]
 7: .001     Mult
 8: 0.0      Offset

88:  End (P95) ; END THERMOCOUPLE AND TDR MEASUREMENTS

89:  Do (P86)
 1: 56       Set Port 6 Low ;; DISABLE CS615s

90:  Beginning of Loop (P87) ; BEGIN HEAT FLUX MEASUREMENT
 1: 0000     Delay
 2: 1        Loop Count

91:  Do (P86)
 1: 75       Pulse Port 5

92:  Excitation with Delay (P22)
 1: 1        Ex Channel
 2: 0000     Delay W/Ex (units = 0.01 sec)
 3: 10       Delay After Ex (units = 0.01 sec)
 4: 0000     mV Excitation

93:  Volt (Diff) (P2) ; HFT-3 SOIL HEAT FLUX PLATE
 1: 1        Reps
 2: 3        25 mV Slow Range
 3: 3        DIFF Channel
 4: 45    -- Loc [ Heatflux  ]
 5: 1.0      Mult
 6: 0.0      Offset

94:  End (P95) ; END HEAT FLUX MEASUREMENT

95:  Do (P86)
 1: 54       Set Port 4 Low ; DISABLE AM416

;CS416 CALCULATIONS
96:  Polynomial (P55) ; 5 cm (WC1)
 1: 1        Reps
 2: 54       X Loc [ TDR7_Per  ]
 3: 64       F(X) Loc [ SoilW_5   ]
 4: -.5699   C0
 5: .9725    C1
 6: -.1893   C2
 7: 0        C3
 8: 0.0      C4
 9: 0.0      C5

97:  Polynomial (P55) ; 10 cm (WC2)
 1: 1        Reps
 2: 55       X Loc [ TDR8_Per  ]
 3: 65       F(X) Loc [ SoilW_10  ] ;
 4: -.7428   C0
 5: 1.28     C1
 6: -.3284   C2
 7: 0        C3
 8: 0.0      C4
 9: 0.0      C5 ;

98:  Polynomial (P55) ; 20 cm (WC3)
 1: 1        Reps
 2: 56       X Loc [ TDR9_Per  ]
 3: 66       F(X) Loc [ SoilW_20  ]
 4: -.9288   C0
 5: 1.7187   C1
 6: -.5797   C2
 7: 0        C3
 8: 0.0      C4
 9: 0.0      C5

99:  Polynomial (P55) ; 50 cm (WC4)
 1: 1        Reps
 2: 57       X Loc [ TDR10_Per ]
 3: 67       F(X) Loc [ SoilW_50  ]
 4: -.9028   C0
 5: 1.5488   C1
 6: -.4178   C2
 7: 0        C3
 8: 0.0      C4
 9: 0.0      C5

100:  Polynomial (P55) ; 100 cm (WC5)
 1: 1        Reps
 2: 58       X Loc [ TDR11_Per ]
 3: 68       F(X) Loc [ SoilW_100 ]
 4: -.4644   C0
 5: .563     C1
 6: .124     C2
 7: 0        C3
 8: 0.0      C4
 9: 0.0      C5

;HEAT FLUX CALCULATIONS
101:  Z=X*F (P37)
 1: 45       X Loc [ Heatflux  ]
 2: 37.6     F
 3: 45       Z Loc [ Heatflux  ]

102:  End (P95) ; END BATTERY, THERMOCOUPLE, TDR, AND HEAT FLUX MEASUREMENTS

;2 MINUTE LICOR OUTPUT
103:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 2        Interval (same units as above)
 3: 10       Set Output Flag High (Flag 0)

104:  Real Time (P77)
 1: 1110     Year,Day,Hour/Minute (midnight = 0000) ;

105:  Sample (P70)
 1: 1        Reps
 2: 24       Loc [ ID        ]

106:  Resolution (P78)
 1: 1        High Resolution

107:  Sample (P70)
 1: 3        Reps
 2: 25       Loc [ Avg_T     ] ;

108:  Average (P71)
 1: 23       Reps
 2: 69       Loc [ PAR_In    ]

;10 MINUTE SOIL/MET SENSOR OUTPUT
109:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 2        Interval (same units as above)
 3: 10       Set Output Flag High (Flag 0)

110:  Set Active Storage Area (P80)
 1: 02       Final Storage Area 2
 2: 2        Array ID

111:  Real Time (P77)
 1: 1110     Year,Day,Hour/Minute (midnight = 0000) ;

112:  Average (P71)
 1: 1        Reps
 2: 30       Loc [ Battery   ]

113:  Average (P71)
 1: 15       Reps
 2: 32       Loc [ AirT_200  ]

114:  Average (P71)
 1: 5        Reps
 2: 64       Loc [ SoilW_5   ]

*Table 2 Program
  02: 120       Execution Interval (seconds)

;RESET COUNTER AFTER 3 HOUR CYCLING IS COMPLETED
1:  If (X<=>F) (P89)
 1: 20       X Loc [ Counter   ]
 2: 1        =
 3: 92       F
 4: 30       Then Do

2:  Z=F (P30)
 1: 0.0      F
 2: 00       Exponent of 10
 3: 20       Z Loc [ Counter   ]

3:  End (P95)

;ADVANCE COUNTER
4:  Z=Z+1 (P32)
 1: 20       Z Loc [ Counter   ]

; ASSIGN COUNT TO SAMPLE/STANDARD
5:  CASE (P93)
 1: 20       Case Loc [ Counter   ]

;1st AIR SAMPLING
6:  If Case Location < F (P83)
 1: 8        F
 2: 1        Call Subroutine 1

;2nd AIR SAMPLING
7:  If Case Location < F (P83)
 1: 9        F
 2: 2        Call Subroutine 2

8:  If Case Location < F (P83)
 1: 15       F
 2: 1        Call Subroutine 1

;1st ZERO WITH REFERENCE GAS
9:  If Case Location < F (P83)
 1: 16       F
 2: 3        Call Subroutine 3

;3rd AIR SAMPLING
10:  If Case Location < F (P83)
 1: 23       F
 2: 1        Call Subroutine 1

;4th AIR SAMPLING
11:  If Case Location < F (P83)
 1: 24       F
 2: 2        Call Subroutine 2

12:  If Case Location < F (P83)
 1: 30       F
 2: 1        Call Subroutine 1

;2nd ZERO WITH REFERENCE GAS
13:  If Case Location < F (P83)
 1: 31       F
 2: 3        Call Subroutine 3

;5th AIR SAMPLING
14:  If Case Location < F (P83)
 1: 38       F
 2: 1        Call Subroutine 1

;6th AIR SAMPLING
15:  If Case Location < F (P83)
 1: 39       F
 2: 2        Call Subroutine 2

16:  If Case Location < F (P83)
 1: 45       F
 2: 1        Call Subroutine 1

;3rd ZERO WITH REFERENCE GAS
17:  If Case Location < F (P83)
 1: 46       F
 2: 3        Call Subroutine 3

;7th AIR SAMPLING
18:  If Case Location < F (P83)
 1: 53       F
 2: 1        Call Subroutine 1

;8th AIR SAMPLING
19:  If Case Location < F (P83)
 1: 54       F
 2: 2        Call Subroutine 2

20:  If Case Location < F (P83)
 1: 60       F
 2: 1        Call Subroutine 1

;4th ZERO WITH REFERENCE GAS
21:  If Case Location < F (P83)
 1: 61       F
 2: 3        Call Subroutine 3

;9th AIR SAMPLING
22:  If Case Location < F (P83)
 1: 68       F
 2: 1        Call Subroutine 1

;10th AIR SAMPLING
23:  If Case Location < F (P83)
 1: 69       F
 2: 2        Call Subroutine 2

24:  If Case Location < F (P83)
 1: 75       F
 2: 1        Call Subroutine 1

;5th ZERO WITH REFERENCE GAS
25:  If Case Location < F (P83)
 1: 76       F
 2: 3        Call Subroutine 3

;11th AIR SAMPLING
26:  If Case Location < F (P83)
 1: 83       F
 2: 1        Call Subroutine 1

;12th AIR SAMPLING
27:  If Case Location < F (P83)
 1: 84       F
 2: 2        Call Subroutine 2

28:  If Case Location < F (P83)
 1: 90       F
 2: 1        Call Subroutine 1

;6th ZERO WITH REFERENCE GAS
29:  If Case Location < F (P83)
 1: 91       F
 2: 3        Call Subroutine 3

;HIGH AND LOW CO2 STANDARDS
30:  If Case Location < F (P83)
 1: 93       F
 2: 4        Call Subroutine 4

31:  End (P95) ;  Case end

;SET ALL RELAY LOCATIONS TO "0"
32:  Do (P86)
 1: 6        Call Subroutine 6

;CLEAR LOOP COUNTER
33:  Z=F (P30)
 1: 0.0      F
 2: 00       Exponent of 10
 3: 23       Z Loc [ Loop      ]

;SET RELAY LOCATIONS (AIR SAMPLES)
34:  If (X<=>F) (P89)
 1: 21       X Loc [ Level     ]
 2: 2        <>
 3: 0.0      F
 4: 30       Then Do

35:  Z=X (P31)
 1: 21       X Loc [ Level     ]
 2: 24       Z Loc [ ID        ]

36:  Beginning of Loop (P87)
 1: 0000     Delay
 2: 7        Loop Count

37:  Z=Z+1 (P32)
 1: 23       Z Loc [ Loop      ]

38:  If (X<=>Y) (P88)
 1: 21       X Loc [ Level     ]
 2: 1        =
 3: 23       Y Loc [ Loop      ]
 4: 30       Then Do

39:  Z=F (P30)
 1: 1        F
 2: 00       Exponent of 10
 3: 9     -- Z Loc [ 2ft       ]

40:  End (P95)

41:  End (P95)

42:  End (P95)

;SET RELAY LOCATIONS (STANDARDS)
43:  If (X<=>F) (P89)
 1: 22       X Loc [ Std       ]
 2: 2        <>
 3: 0.0      F
 4: 30       Then Do

44:  Z=F (P30)
 1: 1        F
 2: 00       Exponent of 10
 3: 8        Z Loc [ Tnk_Swtch ]

ID=Std+7

45:  Beginning of Loop (P87)
 1: 0000     Delay
 2: 3        Loop Count

46:  Z=Z+1 (P32)
 1: 23       Z Loc [ Loop      ]

47:  If (X<=>Y) (P88)
 1: 22       X Loc [ Std       ]
 2: 1        =
 3: 23       Y Loc [ Loop      ]
 4: 30       Then Do

48:  Z=F (P30)
 1: 1        F
 2: 00       Exponent of 10
 3: 16    -- Z Loc [ Ref_gas   ]

49:  End (P95)

50:  End (P95)

51:  End (P95)

;SET RELAY LOCATIONS (POWER)
52:  CASE (P93)
 1: 24       Case Loc [ ID        ]

53:  If Case Location < F (P83)
 1: 2        F
 2: 5        Call Subroutine 5 ;

54:  If Case Location < F (P83)
 1: 8        F
 2: 30       Then Do

55:  Z=F (P30)
 1: 1        F
 2: 00       Exponent of 10
 3: 6        Z Loc [ Air_Pwr   ]

56:  End (P95)

57:  If Case Location < F (P83)
 1: 9        F
 2: 5        Call Subroutine 5

58:  If Case Location < F (P83)
 1: 12       F
 2: 30       Then Do

59:  Z=F (P30)
 1: 1        F
 2: 00       Exponent of 10
 3: 7        Z Loc [ Std_Pwr   ]

60:  End (P95)

61:  End (P95)

;TRIGGER SDM-CD16AC AND POWER DOWN
62:  SDM-CD16 / SDM-CD16AC (P104)
 1: 1        Reps
 2: 0        Address
 3: 4        Loc [ Unused1   ]

63:  Excitation with Delay (P22)
 1: 1        Ex Channel
 2: 7        Delay W/Ex (units = 0.01 sec)
 3: 0000     Delay After Ex (units = 0.01 sec)
 4: 0000     mV Excitation

64:  Do (P86)
 1: 6        Call Subroutine 6

65:  SDM-CD16 / SDM-CD16AC (P104)
 1: 1        Reps
 2: 00       Address
 3: 4        Loc [ Unused1   ]

*Table 3 Subroutines

1:  Beginning of Subroutine (P85)
 1: 1        Subroutine 1

2:  Z=F (P30)
 1: 0.0      F
 2: 00       Exponent of 10
 3: 22       Z Loc [ Std       ]

3:  Z=Z+1 (P32)
 1: 21       Z Loc [ Level     ]

4:  End (P95)

5:  Beginning of Subroutine (P85)
 1: 2        Subroutine 2

6:  Z=F (P30)
 1: 1        F
 2: 00       Exponent of 10
 3: 21       Z Loc [ Level     ]

7:  End (P95)

8:  Beginning of Subroutine (P85)
 1: 3        Subroutine 3

9:  Z=F (P30)
 1: 0.0      F
 2: 00       Exponent of 10
 3: 21       Z Loc [ Level     ]

10:  Z=F (P30)
 1: 1        F
 2: 00       Exponent of 10
 3: 22       Z Loc [ Std       ]

11:  End (P95)

12:  Beginning of Subroutine (P85)
 1: 4        Subroutine 4

13:  Z=Z+1 (P32)
 1: 22       Z Loc [ Std       ]

14:  End (P95)

15:  Beginning of Subroutine (P85)
 1: 5        Subroutine 5

16:  Beginning of Loop (P87)
 1: 0000     Delay
 2: 3        Loop Count

17:  Z=F (P30)
 1: 1        F
 2: 00       Exponent of 10
 3: 5     -- Z Loc [ Tank_Pwr  ]

18:  End (P95)

19:  End (P95)

20:  Beginning of Subroutine (P85)
 1: 6        Subroutine 6

21:  Beginning of Loop (P87)
 1: 0000     Delay
 2: 16       Loop Count

22:  Z=F (P30)
 1: 0.0      F
 2: 00       Exponent of 10
 3: 4     -- Z Loc [ Unused1   ]

23:  End (P95) ;

24:  End (P95)

End Program

-Input Locations-
1 LICOR_T   1 1 1
2 LICOR_P   1 1 1
3 CO2       1 1 1
4 Unused1   1 2 1
5 Tank_Pwr  1 2 1
6 Air_Pwr   1 2 1
7 Std_Pwr   1 2 1
8 Tnk_Swtch 1 2 4
9 2ft       1 2 1
10 5ft       1 2 0
11 10ft      1 2 0
12 25ft      1 2 0
13 45ft      1 2 0
14 70ft      1 2 0
15 97ft      1 2 1
16 Ref_gas   1 2 2
17 Low_std   1 2 1
18 Hi_std    1 2 1
19 Unused2__ 1 2 0
20 Counter   1 2 2
21 Level     1 3 2
22 Std       1 2 3
23 Loop      1 2 3
24 ID        1 2 6
25 Avg_T     1 1 1
26 Avg_P     1 1 1
27 Avg_CO2   1 1 1
28 CSI_R     0 0 0
29 CSI_1     0 0 0
30 Battery   1 1 1
31 Ref_T     1 1 1
32 AirT_200  1 1 1
33 AirT_100  1 1 0
34 AirT_75   1 1 0
35 AirT_50   1 1 0
36 AirT_25   1 1 0
37 SoilT_0   1 1 0
38 SoilT_5   1 1 0
39 SoilT_10  1 1 0
40 SoilT_20  1 1 0
41 SoilT_50  1 1 0
42 SoilT_100 1 1 0
43 TreeT_N   1 1 0
44 TreeT_S   1 1 0
45 Heatflux  1 1 2
46 _________ 1 1 0
47 _________ 0 0 0
48 TDR1_Per  1 0 1
49 TDR2_Per  0 0 0
50 TDR3_Per  1 0 0
51 TDR4_Per  0 0 0
52 TDR5_Per  1 0 0
53 TDR6_Per  1 0 0
54 TDR7_Per  1 0 0
55 TDR8_Per  1 0 0
56 TDR9_Per  1 0 0
57 TDR10_Per 1 0 0
58 TDR11_Per 1 0 0
59 TDR12_Per 1 0 0
60 TDR13_Per 1 0 0
61 TDR14_Per 1 0 0
62 _________ 1 0 0
63 _________ 0 0 0
64 SoilW_5   5 0 1
65 SoilW_10  1 0 1
66 SoilW_20  1 0 1
67 SoilW_50  1 0 1
68 SoilW_100 1 0 1
69 PAR_In    1 1 1
70 PAR_60ft  1 1 1
71 PAR_40ft  1 1 1
72 PAR_25ft  1 1 1
73 PAR_2m    1 1 1
74 WSpd_80ft 1 2 2
75 WSpd_40ft 1 1 1
76 WSpd_2m   1 3 1
77 WDir_80ft 1 1 1
78 WDir_40ft 1 1 1
79 WDir_2m   1 1 1
80 AirT_80ft 1 1 1
81 AirT_60ft 5 1 1
82 AirT_40ft 17 1 1
83 AirT_25ft 5 1 1
84 AirT_2m   17 1 1
85 RH_80ft   5 1 1
86 RH_60ft   17 1 1
87 RH_40ft   5 1 1
88 RH_25ft   17 1 1
89 RH_2m     1 1 1
90 NetRad_2m 1 4 1
91 NetRad2   1 3 4
92 Rn_C      1 2 1
93 Rn_A      1 2 2
94 Rn_B      1 1 1
95 Rn_CrtnFc 1 2 3
96 _________ 0 0 0
97 _________ 0 0 0
98 _________ 0 0 0
99 _________ 0 0 0
100 _________ 0 0 0
101 _________ 0 0 0
102 _________ 0 0 0
103 _________ 1 0 0
-Program Security-
0000
0000
0000
-Mode 4-
-Final Storage Area 2-
300000
-CR10X ID-
0
-CR10X Power Up-
3
