'CR1000
' New Program: c:/pmail/WCrk_SoilSensors_V6.CR1

'last modified by Claire Phillips, June 24, 2013 10:00am cdt
'Prev modification: March 27, 2012 10:00pm cdt

' Commemnts:
' June 24, 2013:
' Swapped FD housings for soil probes June 24, 2013. Calibrated Vaisalas June 23, 2013.
'
' Calibrated probes on March 27. Changed CO2 offsets to 0 and multipliers to 1
' Same as V4, except outputs data every 60min rather than 30 min. Probes on for 20min.
' Calculate soil fluxes using 4 atm references, one for each soil probe'
' Scan multiplexer every 2 minutes, save hourly averages
' Turn CO2 probes on at 40 minutes, average CO2 values from 54-59 minutes to calculate flux
'
' Changes added Oct 25, 2011:
' CO2 calibration modifiers, multiplier and offset
' Measure once per hour, rather than half-hour

'Program: CR1K&9eSE_AM16-32B&107&CS616.CR1
'Date: May 24, 2010
'Measure 8ea CO2 sensors on CR1000
'Measure 8 107 probes on CR1000 
'Measure 16ea 107 probes & 8 ea CS616 on AM16/32B

'----- Equipment Wiring ---------------------------------
'CO2_atm1 Positive (+)  ------------ to ------ CR1000 SE3
'CO2_atm2 +             ------------ to ------ CR1000 SE4
'CO2_atm 1 & 2 Negative (-) -------- to ------ CR1000 Ground (betwwen SE channels)
'CO2_atm3 +             ------------ to ------ CR1000 SE5
'CO2_atm4 +             ------------ to ------ CR1000 SE6
'CO2_atm 3 & 4 -        ------------ to ------ CR1000 Ground (betwwen SE channels)
'CO2_soil1 +             ------------ to ------ CR1000 SE7
'CO2_soil2 +             ------------ to ------ CR1000 SE8
'CO2_soil 1 & 2 -        ------------ to ------ CR1000 Ground (betwwen SE channels)
'CO2_soil 3 +             ------------ to ------ CR1000 SE9
'CO2_soil 4 +             ------------ to ------ CR1000 SE10
'CO2_soil 3 & 4 -        ------------ to ------ CR1000 Ground (betwwen SE channels)
' 107probe#17 Red             ------------ to ------ CR1000 SE11
' 107probe#18 Red              ------------ to ------ CR1000 SE12
' 107probe#17 & #18 purple & clear   ------------ to ------ CR1000 Ground (betwwen SE channels)
' 107probe#19 Red             ------------ to ------ CR1000 SE13
' 107probe#20 Red             ------------ to ------ CR1000 SE14
' 107probe#19 & #20 purple & clear   ------------ to ------ CR1000 Ground (betwwen SE channels)
' 107probe#21 Red             ------------ to ------ CR1000 SE15
' 107probe#22 Red             ------------ to ------ CR1000 SE16
' 107probe#21 & #22 purple & clear   ------------ to ------ CR1000 Ground (betwwen SE channels)
'
' 107probe#17,#18,#19,#20, #21, #22 black   ------------ to ------ CR1000 VX2

'----- Equipment Wiring ---------------------------------
'AM16/32B  --- to --- CR1000
' COM ODD H --------- 1H
' COM ODD L --------- 1L
' COM Ground -------- "G", Ground
' COM EVEN H -------- EX1
' COM EVEN L -------- C4
' 12V  -------------- 12V
' Gnd  -------------- "G", Ground
' Res  -------------- C5
' Clk  -------------- C6
'Note: Set AM16/32B to (4X16) MODE
'
' Sensor Wiring ---------------------------------------
' 107_#1,Red     ------------- to --- AM16/32B, 1H
' CS616_#1 Green ------------- to --- AM16/32B, 1L
' CS616_#1, Black & Clear  --- to --- AM16/32B, Ground
' 107_#1,Black   ------------- to --- AM16/32B, 2H
' CS616_#1,Orange ------------ to --- AM16/32B, 2L
' 107_#1, Purple & Clear ----- to --- AM16/32B, Ground
'*CS616_#1,Red    ------------ to --- CR1000, 12V
' -------------------------// ----------------------
' 107_#16,Red     ------------- to --- AM16/32B, 31H
' CS616_#16 Green ------------- to --- AM16/32B, 31L
' CS616_#16, Black & Clear  --- to --- AM16/32B, Ground
' 107_#16,Black   ------------- to --- AM16/32B, 32H
' CS616_#16,Orange ------------ to --- AM16/32B, 32L
' 107_#16, Purple & Clear ----- to --- AM16/32B, Ground
'*CS616_#16,Red    ------------ to --- CR1000, 12V
'
' *** Note: Connect ALL CS616, Red wire/s to the CR1000, 12V w/ user supplied common tie post.
'
'Declare Variables and Units ----------------------------------------
Dim LCount
Public Flag(4) As Boolean	'Note: User can manually trigger measurements by setting Flag1 High
Public Batt_Volt
Public PTemp_C
Public CO2_atm1
Public CO2_atm2
Public CO2_atm3
Public CO2_atm4
Public CO2_soil1
Public CO2_soil2
Public CO2_soil3
Public CO2_soil4
Public AvgCO2_atm1
Public AvgCO2_atm2
Public AvgCO2_atm3
Public AvgCO2_atm4
Public AvgCO2_soil1
Public AvgCO2_soil2
Public AvgCO2_soil3
Public AvgCO2_soil4
Public Flux_1
Public Flux_2
Public Flux_3
Public Flux_4
Public T107_C(22)
Public VWC(16)
Public PA_uS(16)

Const High = true
Const Low = false

'Housing=NL-0610-03  Pit=1  G=0.047332  Intercept= -0.002995 Cal June2013
Const GParam1=0.047332
Const Offset1=-0.002995

'Housing=NL-0610-09  Pit=2  G=0.038879   Intercept= -0.000980 Cal June2013
Const GParam2=0.038879
Const Offset2=-0.000980

'Housing=NL-0610-31  Pit=3  G=0.039835  Intercept= -0.019667 Cal June2013
Const GParam3=0.04506416
Const Offset3=-0.06925746

'Housing=CLP-0211-14  Pit=4  G=0.051018   Intercept= 0.029024 Cal June2013
Const GParam4=0.04794814
Const Offset4=-0.173038

'Calibration factors, measured 10/23-25/2011 at Kemp NRS
'Probe = Soil1, SN=F5240008
  Const CO2Mult_soil1=1
  Const CO2Off_soil1=0
'Probe=Atm1, SN=G3440005
  Const CO2Mult_atm1=1
  Const CO2Off_atm1=0

'Probe = Soil2, SN=F5240001
  Const CO2Mult_soil2=1
  Const CO2Off_soil2=0
'Probe=Atm2, SN=G351001
  Const CO2Mult_atm2=1
  Const CO2Off_atm2=0

'Probe = Soil3, SN=F5230009
  Const CO2Mult_soil3=1
  Const CO2Off_soil3=0
'Probe=Atm3, SN=F5220008
  Const CO2Mult_atm3=1
  Const CO2Off_atm3=0

'Probe = Soil4, SN=F5220010
  Const CO2Mult_soil4=1
  Const CO2Off_soil4=0
'Probe=Atm4, SN=G3440004
  Const CO2Mult_atm4=1
  Const CO2Off_atm4=0

Units Batt_Volt=Volts
Units PTemp_C=Deg C
Units T107_C=Deg C
Units PA_uS=uSec
Units VWC=volumetric water content
Units CO2_atm1=ppm
Units CO2_atm2=ppm
Units CO2_atm3=ppm
Units CO2_atm4=ppm
Units CO2_soil1=ppm
Units CO2_soil2=ppm
Units CO2_soil3=ppm
Units CO2_soil4=ppm
Units AvgCO2_atm1=ppm
Units AvgCO2_atm2=ppm
Units AvgCO2_atm3=ppm
Units AvgCO2_atm4=ppm
Units AvgCO2_soil1=ppm
Units AvgCO2_soil2=ppm
Units AvgCO2_soil3=ppm
Units AvgCO2_soil4=ppm
Units Flux_1=umol/m2/s
Units Flux_2=umol/m2/s
Units Flux_3=umol/m2/s
Units Flux_4=umol/m2/s

' 30_Minute Data Table ----------------------------------------------
DataTable(Dat_60min,True,-1)
  DataInterval(0,60,Min,10)
  Sample(1,Batt_Volt,FP2)
  Sample(1,Flux_1,FP2)
  Sample(1,Flux_2,FP2)
  Sample(1,Flux_3,FP2)
  Sample(1,Flux_4,FP2)
  Sample(1,AvgCO2_atm1,FP2)  
  Sample(1,AvgCO2_atm2,FP2)  
  Sample(1,AvgCO2_atm3,FP2)  
  Sample(1,AvgCO2_atm4,FP2)  
  Sample(1,AvgCO2_soil1,FP2)
  Sample(1,AvgCO2_soil2,FP2)
  Sample(1,AvgCO2_soil3,FP2)
  Sample(1,AvgCO2_soil4,FP2)
  Average(22,T107_C(1),FP2,False)
  Average(8,VWC(1),FP2,False)
  'Average(8,PA_uS(1),FP2,False)
EndTable


' Main Program ------------------------------------------------------
BeginProg
  Scan(30,Sec,1,0)
    Battery(Batt_Volt)	'Battery Voltage measurement Batt_Volt:
    PanelTemp(PTemp_C,_60Hz)	'Wiring Panel Temperature measurement PTemp_C:
    '
    VoltSe (CO2_atm1,1,mV5000,3,1,0,250,1.0,0) 
    VoltSe (CO2_atm2,1,mV5000,4,1,0,250,1.0,0) 
    VoltSe (CO2_atm3,1,mV5000,5,1,0,250,1.0,0) 
    VoltSe (CO2_atm4,1,mV5000,6,1,0,250,1.0,0) 
    VoltSe (CO2_soil1,1,mV5000,7,1,0,250,1.0,0) 
    VoltSe (CO2_soil2,1,mV5000,8,1,0,250,1.0,0)
    VoltSe (CO2_soil3,1,mV5000,9,1,0,250,1.0,0)
    VoltSe (CO2_soil4,1,mV5000,10,1,0,250,1.0,0)
    
    CO2_atm1 = CO2_atm1 * CO2Mult_atm1 + CO2Off_atm1
    CO2_atm2 = CO2_atm2 * CO2Mult_atm2 + CO2Off_atm2
    CO2_atm3 = CO2_atm3 * CO2Mult_atm3 + CO2Off_atm3
    CO2_atm4 = CO2_atm4 * CO2Mult_atm4 + CO2Off_atm4
    CO2_soil1 = CO2_soil1 * CO2Mult_soil1 + CO2Off_soil1
    CO2_soil2 = CO2_soil2 * CO2Mult_soil2 + CO2Off_soil2
    CO2_soil3 = CO2_soil3 * CO2Mult_soil3 + CO2Off_soil3
    CO2_soil4 = CO2_soil4 * CO2Mult_soil4 + CO2Off_soil4

    AvgRun(AvgCO2_atm1,1,CO2_atm1,10)
    AvgRun(AvgCO2_atm2,1,CO2_atm2,10)
    AvgRun(AvgCO2_atm3,1,CO2_atm3,10)
    AvgRun(AvgCO2_atm4,1,CO2_atm4,10)
    AvgRun(AvgCO2_soil1,1,CO2_soil1,10)
    AvgRun(AvgCO2_soil2,1,CO2_soil2,10)
    AvgRun(AvgCO2_soil3,1,CO2_soil3,10)
    AvgRun(AvgCO2_soil4,1,CO2_soil4,10)

    Therm107 (T107_C(17),6,11,Vx2,0,250,1.0,0)  'measure 6ea 107 probes on SE channel #11,12,13,14,15,16
    '
    If IfTime(0,2,Min) Then
      Flag(1) = High	'Note: User can manually set this Flag High anytime!
    EndIf
    '
    If Flag(1)=High Then 'User can manually trigger measurements by setting Flag1 High
      PortSet(5,1)	'Switch AM16/32 Multiplexer Reset line from low To high
      LCount=1
      '
      SubScan(0,uSec,16)
        PulsePort(6,10000)	'Clock/Switch AM16/32 Multiplexer channel
        Therm107(T107_C(LCount),1,1,1,0,_60Hz,1,0)	'measure 1ea 107 probe
        CS616(PA_uS(LCount),1,2,4,1,1,0)	'measure 1ea CS616 probe
        LCount=LCount+1
      NextSubScan
      '
      For LCount=1 To 16	'convert CS616 period to volumetric water content
        VWC(LCount)=-0.0663+(-0.0063*PA_uS(LCount))+(0.0007*PA_uS(LCount)^2)
      Next
      '
      PortSet(5,0)	'Switch AM16/32 Reset line low
      Flag(1)= Low	'Set Flag(1)=low
    EndIf
    
    If IfTime(40,60,Min) Then
      PortSet(8,1)
     EndIf   
   
   If IfTime(59,60,Min) Then
    'Edit to atm1, atm2, atm3, and atm4 when atm housings arrive.
      Flux_1=((AvgCO2_soil1-AvgCO2_atm1)*GParam1)+Offset1
      Flux_2=((AvgCO2_soil2-AvgCO2_atm2)*GParam2)+Offset2
      Flux_3=((AvgCO2_soil3-AvgCO2_atm3)*GParam3)+Offset3
      Flux_4=((AvgCO2_soil4-AvgCO2_atm4)*GParam4)+Offset4
    EndIf 
   
   If IfTime(0,60,Min) Then
      PortSet(8,0)
     EndIf   
    
'Call Data Tables and Store Data -------------
    CallTable(Dat_60min)


   
  NextScan
EndProg

