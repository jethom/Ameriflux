;{CR10X}
;Program:     (site)
;Location:    (city,state)
;RSP:         PYxxxxx; CC=xx.x; R=100; CF=xxx.xx
;Lat:         xx.xxx N
;Long:        xx.xxx W
;
;Program Revisions
;****REMEMBER TO CHANGE PROGRAM REV # IN INSTRUCTION #1
;****WHEN MAKING MODIFICATIONS TO THIS PROGRAM.
;** Edit FSA1 output array ID to match site code
;This is typically instruction # 143 in Table 1.
;
;Rev  Date         Description
;0    01/23/96     Standard program revised by PJB
;1    06/20/96     Error flag code added by RDL
;2    06/20/96     Instructions added by DLG to disable
;rotation when Vbat<10.5V.
;3    03/07/97     Changed C2 to always be input port
;                  Changed pulse routine to pulse for 500 counts
;                  and then up to an additional 500 depending on state of C2.
;                  Removed signature instruction (RDL)
;4    03/19/97     Added REV, Changed "Error on" to flag from #pulses (RDL)
;                  Also fixed input locations from CSI conversion
;5    11/11/97     Put in a minimum pulse length of 500 loops (RDL)
;6    xx/xx/xx     Initial Program (your initials here)
;
;
;Hardware Input Channel Connections:
;
;Analog
;1H SE  107   Red
;AG     107   Purple
;3H SE  RSP   White
;AG     RSP   Yellow
;
;Excitation
;E1     107   Black
;
;Control
;C1     Motor Rotation Control   Brown
;C2     Motor Positon Feedback   Blue
;
;Flags
;F1      Rotate when high
;F2      High when rotation finished
;F3      High if rotational error
;F4      High if rotational error from previous interval
;
*Table 1 Program
  01: 1.0       Execution Interval (seconds)
;
;Set site constants
;
; Increment Revision number after every program change
1:  Z=F (P30)
 1: 0        F ; *** enter program revision here ***
 2: 00       Exponent of 10
 3: 18       Z Loc [ Revision  ]

2:  Z=F (P30)
 1: 458.6    F; *** enter pyranometer calibration factor here
 2: 00       Exponent of 10
 3: 45       Z Loc [ GH_____CF ]; W/m2 / mV

3:  Z=F (P30)
 1: 1        F ;*** enter latitude here ***
 2: 0        Exponent of 10
 3: 49       Z Loc [ Latitude  ]; degrees North

4:  Z=F (P30)
 1: 1        F ;*** enter lognitude here ***
 2: 0        Exponent of 10; degrees West
 3: 50       Z Loc [ Longitude ]
;(for sites east of Greenwich, UK, use negative degrees)

5:  Z=F (P30); Hours behind Greenwich Mean Time
 1: 0        F; *** enter correct time zone: 5,6,7,8 = EST,CST,MST,PST***
 2: 00       Exponent of 10
 3: 51       Z Loc [ Time_Zone ]
;(for sites east of Greenwich use negative time zones)
;
;
;Initialize ports
;

6:  Set Port(s) (P20)
 1: 9999     C8,C7,C6,C5 Options
 2: 9980     C4..C1 = nc/nc/input/0
;
;Measure 1 second data
;

7:  Volts (SE) (P1)
 1: 1        Reps
 2: 1        2.5 mV Slow Range
 3: 5        SE Channel
 4: 56       Loc [ GH_____mV ]
 5: -1       Mult; change this to "1" if GH reads negative
 6: 0.0000   Offset; also: be sure to change Mult in Burst sample
;(usually instruction 32)

8:  Temp (107) (P11)
 1: 1        Reps
 2: 1        SE Channel
 3: 01       Excite all reps w/Exchan 1
 4: 4        Loc [ AT______F ]
 5: 1.8      Mult
 6: 32       Offset
;
;Multiply by calibration factor
;

9:  Z=X*Y (P36)
 1: 56       X Loc [ GH_____mV ]
 2: 45       Y Loc [ GH_____CF ]
 3: 1        Z Loc [ GH___W_m2 ]
;
;measure CR10 voltage

10:  Batt Voltage (P10)
 1: 12       Loc [ Vbat____V ]
;
;Set the rotation flag (F1) when it is 54 seconds
;into the minute (first condition for rotation)
;

11:  Time (P18)
 1: 0        Seconds into current minute (maximum 60)
 2: 60       Mod/By
 3: 34       Loc [ t0        ]

12:  IF (X<=>F) (P89)
 1: 34       X Loc [ t0        ]
 2: 4        <
 3: 54       F
 4: 22       Set Flag 2 Low

13:  IF (X<=>F) (P89)
 1: 34       X Loc [ t0        ]
 2: 3        >=
 3: 54       F
 4: 30       Then Do

14:  If Flag/Port (P91)
 1: 22       Do if Flag 2 is Low
 2: 11       Set Flag 1 High; (only at 54 sec)

15:  End (P95)
;
;Calculate LST in degrees
;

16:  Z=X*F (P37)
 1: 34       X Loc [ t0        ]
 2: .01667   F
 3: 34       Z Loc [ t0        ]

17:  Time (P18)
 1: 1        Minutes into current day (maximum 1440)
 2: 0000     Mod/By
 3: 35       Loc [ t1        ]

18:  Z=X+Y (P33)
 1: 34       X Loc [ t0        ]
 2: 35       Y Loc [ t1        ]
 3: 35       Z Loc [ t1        ]

19:  Z=X*F (P37)
 1: 35       X Loc [ t1        ]
 2: .25      F
 3: 53       Z Loc [ LST___deg ]
;
;Calculate Cos(Zen) = f(lat,long,time,date)
;

20:  Do (P86)
 1: 1        Call Subroutine 1

21:  If Flag/Port (P91)
 1: 11       Do if Flag 1 is High
 2: 30       Then Do; (if 54 seconds into minute)
;
;Set default diffuse horizontal
;

22:  Z=X (P31)
 1: 56       X Loc [ GH_____mV ]
 2: 58       Z Loc [ DH_____mV ]

23:  If Flag/Port (P91)
 1: 11       Do if Flag 1 is High
 2: 12       Set Flag 2 High; (high from 54 - 60 sec)

24:  If Flag/Port (P91)
 1: 11       Do if Flag 1 is High
 2: 21       Set Flag 1 Low; (high only at 54 seconds)
;
;Set previous rotation error flag = current rot err flag
;(F4 = F3), then reset current rot err flag
;

25:  If Flag/Port (P91)
 1: 13       Do if Flag 3 is High
 2: 14       Set Flag 4 High

26:  If Flag/Port (P91)
 1: 23       Do if Flag 3 is Low
 2: 24       Set Flag 4 Low

27:  If Flag/Port (P91)
 1: 13       Do if Flag 3 is High
 2: 23       Set Flag 3 Low
;
;Rotate on the 1/2 hr at night (GH < 5 W/m2)
;

28:  IF (X<=>F) (P89)
 1: 1        X Loc [ GH___W_m2 ]
 2: 4        <
 3: 5        F
 4: 30       Then Do

29:  If time is (P92)
 1: 0000     Minutes (Seconds --) into a
 2: 30       Interval (same units as above)
 3: 30       Then Do

30:  Do (P86)
 1: 2        Call Subroutine 2; (rotate)

31:  End (P95)

32:  Else (P94); GH > 5 W/m2

33:  Do (P86)
 1: 2        Call Subroutine 2; (rotate)
;
;Make the Burst measurement
;

34:  Burst Measurement (P23)
 1: 1        Input Channels per Scan
 2: 13       ñ 25 mV Fast Range
 3: 5        In Chan
 4: 0001     Trig/Trig/Dest/Meas Options
 5: 2.0      Time per Scan (msec); 1.4 seconds
 6: 0.7      Scans (in thousands); 700 samples
 7: 0000     Samples before Trigger
 8: 0.0000   mV Limit
 9: 0000     mV Excitation
 10: 250     Loc [ Burst_Dat ]
 11: -1      Mult
 12: 0.0000  Offset
;
;Estimate the noise and signal fluctuation from the
;first 25 samples in the burst mode.   A calculation
;of the peak to peak fluctuation is made in units of
;W/m2.
;

35:  Spatial Mimimum (P50)
 1: 25       Swath
 2: 250      First Loc [ Burst_Dat ]
 3: 34       Min Option [ t0        ]

36:  Spatial Maximum (P49)
 1: 25       Swath
 2: 250      First Loc [ Burst_Dat ]
 3: 35       Max Option [ t1        ]

37:  Z=X-Y (P35)
 1: 35       X Loc [ t1        ]
 2: 34       Y Loc [ t0        ]
 3: 35       Z Loc [ t1        ]

38:  Z=X*Y (P36)
 1: 35       X Loc [ t1        ]
 2: 45       Y Loc [ GH_____CF ]
 3: 65       Z Loc [ GHrn_W_m2 ]
;
;
;The burst data indicies are:
;
;250 to  274    GHr I
;275 to  924    Curve data
;925 to  949    GHr II
;
;Find the minimum value in the burst data
;between the indicies of 275 and 924.  Identify the
;index of the minimum.
;

39:  Spatial Mimimum (P50)
 1: 650      Swath
 2: 275      First Loc [ Curve_Dat ]
 3: 1028     Min Option [ min_val   ]
;
;Check for a Burst Data Error
;

40:  Spatial Mimimum (P50); of last 25 samples
 1: 25       Swath
 2: 925      First Loc [ GHII      ]
 3: 35       Min Option [ t1        ]

41:  Z=X (P31)
 1: 28       X Loc [ min_val   ]
 2: 36       Z Loc [ t2        ]; Minimum of entire well

42:  Spatial Mimimum (P50)
 1: 3        Swath
 2: 34       First Loc [ t0        ]; has min of 1st 25 samples
 3: 34       Min Option [ t0        ]

43:  IF (X<=>F) (P89)
 1: 34       X Loc [ t0        ]
 2: 4        <
 3: -0.2     F; (in mV)
 4: 30       Then Do

44:  Z=Z+1 (P32); If any BD data pts are < -0.2 then
 1: 15       Z Loc [ Err_bd    ]; increment BD err count

45:  Z=Z+1 (P32)
 1: 16       Z Loc [ Err_flag  ]; Add one to err flag

46:  If Flag/Port (P91)
 1: 23       Do if Flag 3 is Low
 2: 13       Set Flag 3 High; If BD error occurred

47:  End (P95)
;
;Continue if there were no rotation errors.
;

48:  If Flag/Port (P91)
 1: 23       Do if Flag 3 is Low
 2: 30       Then Do
;
;Determine the GH from the rotation data by averaging
;the first and last 25 samples
;

49:  Spatial Average (P51)
 1: 25       Swath
 2: 250      First Loc [ Burst_Dat ]
 3: 36       Avg Loc [ t2        ]

50:  Spatial Average (P51)
 1: 25       Swath
 2: 925      First Loc [ GHII      ]
 3: 37       Avg Loc [ t3        ]

51:  Spatial Average (P51)
 1: 2        Swath
 2: 36       First Loc [ t2        ]
 3: 66       Avg Loc [ GH_rot_mV ]
;
;Move the 50 points around the min to locations
;100 to 149.  Loc 125 will have the min value.
;

52:  Z=F (P30)
 1: 100      F
 2: 00       Exponent of 10
 3: 34       Z Loc [ t0        ]

53:  Z=X+F (P34)
 1: 29       X Loc [ min_ind   ]
 2: -25      F
 3: 35       Z Loc [ t1        ]

54:  Beginning of Loop (P87)
 1: 0000     Delay
 2: 50       Loop Count

55:  Indirect Move (P61)
 1: 35    -- Source Loc [ t1        ]
 2: 34    -- Destination Loc [ t0        ]

56:  End (P95)
;
;Calculate the delta array using locs 150-198
;

57:  Beginning of Loop (P87)
 1: 0000     Delay
 2: 49       Loop Count

58:  Z=X-Y (P35)
 1: 101   -- X Loc [ _________ ]
 2: 100   -- Y Loc [ Well_Arr  ]
 3: 150   -- Z Loc [ Delta_Arr ]

59:  End (P95)

60:  Spatial Mimimum (P50)
 1: 49       Swath
 2: 150      First Loc [ Delta_Arr ]
 3: 1024     Min Option [ x1__val   ]

61:  Spatial Maximum (P49)
 1: 49       Swath
 2: 150      First Loc [ Delta_Arr ]
 3: 1026     Max Option [ x2__val   ]
;
;Determine x1 and x2 indicies in terms of the well array
;stored in 100-149.
;

62:  Z=X+F (P34)
 1: 25       X Loc [ x1__ind   ]
 2: -50      F
 3: 25       Z Loc [ x1__ind   ]

63:  Z=X+F (P34)
 1: 27       X Loc [ x2__ind   ]
 2: -49      F
 3: 27       Z Loc [ x2__ind   ]
;
;X1 and x2 indicies should be with indicies 100 to 149
;Well center is halfway between x1 and x2
;

64:  Z=X+Y (P33)
 1: 25       X Loc [ x1__ind   ]
 2: 27       Y Loc [ x2__ind   ]
 3: 34       Z Loc [ t0        ]

65:  Z=X*F (P37)
 1: 34       X Loc [ t0        ]
 2: 0.5      F
 3: 31       Z Loc [ cen_ind   ]
;
;Store the center value
;

66:  Z=F (P30)
 1: 30       F
 2: 00       Exponent of 10
 3: 34       Z Loc [ t0        ]

67:  Indirect Move (P61)
 1: 31       Source Loc [ cen_ind   ]
 2: 34       Destination Loc [ t0        ]
;
;Calculate the width of the well
;

68:  Z=X-Y (P35)
 1: 27       X Loc [ x2__ind   ]
 2: 25       Y Loc [ x1__ind   ]
 3: 23       Z Loc [ Well_wdth ]
;
;Calculate the diffuse horizontal component if well
;width is >= 5.  S1 and s2 are well shoulder values
;
;DH = (GH - avg[s1,s2]) + well center value
;
;where (GH - avg[s1,s2]) is diffuse occluded by band

69:  IF (X<=>F) (P89)
 1: 23       X Loc [ Well_wdth ]
 2: 3        >=
 3: 5        F
 4: 30       Then Do

70:  Z=X+Y (P33)
 1: 66       X Loc [ GH_rot_mV ]
 2: 30       Y Loc [ cen_val   ]
 3: 37       Z Loc [ t3        ]
;
;Place s1 value in loc 32; s1 index is at well center
;minus well width

71:  Z=F (P30)
 1: 32       F
 2: 00       Exponent of 10
 3: 35       Z Loc [ t1        ]

72:  Z=X-Y (P35)
 1: 31       X Loc [ cen_ind   ]
 2: 23       Y Loc [ Well_wdth ]
 3: 36       Z Loc [ t2        ]

73:  IF (X<=>F) (P89); If s1 falls before well begins,
 1: 36       X Loc [ t2        ]; place s1 at well beginning
 2: 4        <
 3: 100      F
 4: 30       Then Do

74:  Z=F (P30)
 1: 100      F
 2: 00       Exponent of 10
 3: 36       Z Loc [ t2        ]

75:  End (P95)

76:  Indirect Move (P61); Move value of well shoulder to loc 32
 1: 36       Source Loc [ t2        ]
 2: 35       Destination Loc [ t1        ]
;
;Place the value of s2 in loc 33
;

77:  Z=Z+1 (P32)
 1: 35       Z Loc [ t1        ]

78:  Z=X+Y (P33)
 1: 31       X Loc [ cen_ind   ]
 2: 23       Y Loc [ Well_wdth ]
 3: 36       Z Loc [ t2        ]

79:  IF (X<=>F) (P89); If s2 falls after well ends, place s2 at
 1: 36       X Loc [ t2        ]; end of well
 2: 3        >=
 3: 149      F
 4: 30       Then Do

80:  Z=F (P30)
 1: 149      F
 2: 00       Exponent of 10
 3: 36       Z Loc [ t2        ]

81:  End (P95)

82:  Indirect Move (P61)
 1: 36       Source Loc [ t2        ]
 2: 35       Destination Loc [ t1        ]
;
;Average s1 and s2
;

83:  Spatial Average (P51)
 1: 2        Swath
 2: 32       First Loc [ s1__val   ]
 3: 35       Avg Loc [ t1        ]

84:  Z=X-Y (P35)
 1: 37       X Loc [ t3        ]
 2: 35       Y Loc [ t1        ]
 3: 58       Z Loc [ DH_____mV ]

85:  End (P95); If well width > 5

86:  End (P95); If no burst data pts < -0.2

87:  End (P95); If GH > 5 W/m2

88:  End (P95); If 54 seconds into minute
;
;
;********      End of the Rotation Loop   ********
;
;
;Set default DN and calculate DN = (GH - DH)/cos(zen)
;

89:  Z=F (P30)
 1: 0.0000   F
 2: 00       Exponent of 10
 3: 57       Z Loc [ DN_____mV ]

90:  IF (X<=>F) (P89); Calculate DN only if sun is
 1: 43       X Loc [ cos_zen_  ]; 1.15 deg above horizon
 2: 3        >=
 3: 0.02     F
 4: 30       Then Do

91:  IF (X<=>F) (P89); Calculate DN only if GH > 5 W/m2
 1: 1        X Loc [ GH___W_m2 ]
 2: 3        >=
 3: 5        F
 4: 30       Then Do

92:  Z=X-Y (P35)
 1: 56       X Loc [ GH_____mV ]
 2: 58       Y Loc [ DH_____mV ]
 3: 34       Z Loc [ t0        ]

93:  Z=X/Y (P38)
 1: 34       X Loc [ t0        ]
 2: 43       Y Loc [ cos_zen_  ]
 3: 57       Z Loc [ DN_____mV ]

94:  End (P95); If GH > 5 W/m2

95:  End (P95); If cos(zen) > 0.02

96:  Z=X*Y (P36)
 1: 57       X Loc [ DN_____mV ]
 2: 45       Y Loc [ GH_____CF ]
 3: 2        Z Loc [ DN___W_m2 ]

97:  Z=X*Y (P36)
 1: 58       X Loc [ DH_____mV ]
 2: 45       Y Loc [ GH_____CF ]
 3: 3        Z Loc [ DH___W_m2 ]
;
;************ Begin 1 minute data processing ***********
;
;Calculate 1-minute averages of the first 5 data fields.
;This usually will include GH,DN,DH and POA, whose 1-min
;averages will be set to zero if -5 < x < 1.  Since AT
;is usually part of this group, it is important to
;note that the 1-minute average "zeroed" location should
;NOT be used for output to final storage area 1.
;"Zeroing" is desired for irradiance measurements to
;retrieve clean "0" values throughout the night.
;Note:  zeroing range prior to 01/23/96: -20 < x < 0.
;
;Place the 1-minute averages into locations 67..71

98:  If time is (P92)
 1: 0000     Minutes (Seconds --) into a
 2: 1        Interval (same units as above)
 3: 10       Set Output Flag High

99:  Set Active Storage Area (P80)
 1: 3        Input Storage Area
 2: 67       Array ID or Loc [ _GH__W_m2 ]

100:  Average (P71)
 1: 5        Reps
 2: 1        Loc [ GH___W_m2 ]
;
;Range checking: if -5 <= x < 1 then x := 0
;(only perform if on the minute)

101:  If Flag/Port (P91)
 1: 10       Do if Output Flag is High (Flag 0)
 2: 30       Then Do

102:  Beginning of Loop (P87)
 1: 0000     Delay
 2: 5        Loop Count

103:  IF (X<=>F) (P89)
 1: 67    -- X Loc [ _GH__W_m2 ]
 2: 4        <
 3: 1        F
 4: 30       Then Do

104:  IF (X<=>F) (P89)
 1: 67    -- X Loc [ _GH__W_m2 ]
 2: 3        >=
 3: -5       F
 4: 30       Then Do

105:  Z=F (P30)
 1: 0.0000   F
 2: 00       Exponent of 10
 3: 67    -- Z Loc [ _GH__W_m2 ]

106:  End (P95)

107:  End (P95)

108:  End (P95); loop
;
;If DN > 1 W/m2 then calculate a DN correction factor
;to account for a slowly time varying DH
;
;C =  { _GH - avg(_DH,DH) } / { _GH - _DH }
;
;so:  C > 1 if DH is dropping, < 1 otherwise

109:  Z=F (P30)
 1: 0.0000   F
 2: 00       Exponent of 10
 3: 38       Z Loc [ C         ]

110:  Z=X+Y (P33)
 1: 69       X Loc [ _DH__W_m2 ]
 2: 3        Y Loc [ DH___W_m2 ]
 3: 34       Z Loc [ t0        ]

111:  Z=X*F (P37)
 1: 34       X Loc [ t0        ]
 2: 0.5      F
 3: 34       Z Loc [ t0        ]

112:  IF (X<=>F) (P89)
 1: 68       X Loc [ _DN__W_m2 ]
 2: 3        >=
 3: 1.0      F
 4: 30       Then Do

113:  Z=X-Y (P35)
 1: 67       X Loc [ _GH__W_m2 ]
 2: 34       Y Loc [ t0        ]
 3: 35       Z Loc [ t1        ]

114:  Z=X-Y (P35)
 1: 67       X Loc [ _GH__W_m2 ]
 2: 69       Y Loc [ _DH__W_m2 ]
 3: 36       Z Loc [ t2        ]

115:  Z=X/Y (P38)
 1: 35       X Loc [ t1        ]
 2: 36       Y Loc [ t2        ]
 3: 38       Z Loc [ C         ]

116:  End (P95)
;
;Range checking for C:  0 < C < 1.5
;

117:  IF (X<=>F) (P89)
 1: 38       X Loc [ C         ]
 2: 4        <
 3: 0.0000   F
 4: 30       Then Do

118:  Z=F (P30)
 1: 0.0000   F
 2: 00       Exponent of 10
 3: 38       Z Loc [ C         ]

119:  End (P95)

120:  IF (X<=>F) (P89)
 1: 38       X Loc [ C         ]
 2: 3        >=
 3: 1.5      F
 4: 30       Then Do

121:  Z=F (P30)
 1: 1.5      F
 2: 00       Exponent of 10
 3: 38       Z Loc [ C         ]

122:  End (P95)

123:  Z=X*Y (P36)
 1: 68       X Loc [ _DN__W_m2 ]
 2: 38       Y Loc [ C         ]
 3: 68       Z Loc [ _DN__W_m2 ]

124:  Z=X (P31); (_DH := avg(DH,_DH)
 1: 34       X Loc [ t0        ]
 2: 69       Z Loc [ _DH__W_m2 ]
;
;If DH > GH then DN = 0, DH = GH.
;

125:  IF (X<=>Y) (P88)
 1: 69       X Loc [ _DH__W_m2 ]
 2: 3        >=
 3: 67       Y Loc [ _GH__W_m2 ]
 4: 30       Then Do

126:  Z=F (P30)
 1: 0.0000   F
 2: 00       Exponent of 10
 3: 68       Z Loc [ _DN__W_m2 ]

127:  Z=X (P31)
 1: 67       X Loc [ _GH__W_m2 ]
 2: 69       Z Loc [ _DH__W_m2 ]

128:  End (P95)
;
;Re-range check _DH:  If -5 =< DH < 1 then DH = 0
;

129:  IF (X<=>F) (P89)
 1: 69       X Loc [ _DH__W_m2 ]
 2: 4        <
 3: 1        F
 4: 30       Then Do

130:  IF (X<=>F) (P89)
 1: 69       X Loc [ _DH__W_m2 ]
 2: 3        >=
 3: -5       F
 4: 30       Then Do

131:  Z=F (P30)
 1: 0.0000   F
 2: 00       Exponent of 10
 3: 69       Z Loc [ _DH__W_m2 ]

132:  End (P95)

133:  End (P95); of range checking

134:  End (P95); if on the minute

135:  Do (P86)
 1: 20       Set Output Flag Low
;
;********** End of 1 minute data processing ************
;
;Send data to final storage area 1 at specified interval
;CC,YR,JD,HM,GH,DN,DH,AT
;

136:  Set Active Storage Area (P80)
 1: 1        Final Storage Area 1
 2: 1        Array ID or Loc [ _________ ]; Enter site code here

137:  If time is (P92)
 1: 0000     Minutes (Seconds --) into a
 2: 15       Interval (same units as above)
 3: 10       Set Output Flag High

138:  Sample (P70)
 1: 1        Reps
 2: 18       Loc [ Revision  ]

139:  Real Time (P77)
 1: 1220     Year,Day,Hour/Minute (prev day at midnight, 2400 at midnight); 2400 at midnight

140:  Average (P71)
 1: 4        Reps
 2: 67    -- Loc [ _GH__W_m2 ]
;
;Battery check

141:  If Flag/Port (P91)
 1: 10       Do if Output Flag is High (Flag 0)
 2: 30       Then Do

142:  IF (X<=>F) (P89)
 1: 12       X Loc [ Vbat____V ]
 2: 4        <
 3: 11.5     F
 4: 30       Then Do

143:  Z=X+F (P34)
 1: 16       X Loc [ Err_flag  ]
 2: 4096     F
 3: 16       Z Loc [ Err_flag  ]

144:  End (P95); End of battery check
;
;Output error flag using high resolution

145:  Resolution (P78)
 1: 1        high resolution

146:  Sample (P70)
 1: 1        Reps
 2: 16       Loc [ Err_flag  ]

147:  Resolution (P78)
 1: 0        low resolution
;
;Zero error flag

148:  Z=F (P30)
 1: 0.0000   F
 2: 00       Exponent of 10
 3: 16       Z Loc [ Err_flag  ]

149:  End (P95); End of error flag output

150:  Do (P86)
 1: 20       Set Output Flag Low
;
;Output hourly system diagnostics information to FSA2:
;CC,JD,HM,Vb,sig,min ind,well width,C,err tot

151:  Set Active Storage Area (P80)
 1: 2        Final Storage Area 2
 2: 511      Array ID or Loc [ _________ ]

152:  If time is (P92)
 1: 0000     Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 10       Set Output Flag High

153:  Real Time (P77)
 1: 1220     Year,Day,Hour/Minute (prev day at midnight, 2400 at midnight); 2400 at midnight

154:  Average (P71)
 1: 1        Reps
 2: 12       Loc [ Vbat____V ]

155:  Average (P71)
 1: 1        Reps
 2: 29       Loc [ min_ind   ]

156:  Average (P71)
 1: 1        Reps
 2: 23       Loc [ Well_wdth ]

157:  Average (P71)
 1: 1        Reps
 2: 38       Loc [ C         ]

158:  Sample (P70)
 1: 1        Reps
 2: 16       Loc [ Err_flag  ]

*Table 2 Program
  02: 0         Execution Interval (seconds)

*Table 3 Subroutines
;
;Calculate Cos(Zen) as a fn(lat,long,time,date)
;CR10 execution time is estimated to be 90 msec
;

1:  Beginning of Subroutine (P85)
 1: 1        Subroutine 1
;
;Determine day of year and subtract 1
;so that Jan 1st (JD-1) = 0
;

2:  Time (P18)
 1: 2        Hours into current year (maximum 8748)
 2: 0        Mod/By
 3: 52       Loc [ JD_1      ]

3:  Z=X*F (P37)
 1: 52       X Loc [ JD_1      ]; (divide by 24 hrs/day)
 2: .04167   F
 3: 52       Z Loc [ JD_1      ]

4:  Z=INT(X) (P45)
 1: 52       X Loc [ JD_1      ]
 2: 52       Z Loc [ JD_1      ]
;
;Now calc EOT
;
;EOT(degs) = AVG[   7.416 sin( .98549(JD-1)     )
;-0.258 sin( .98549(JD-1) + 90)
;9.228 sin( 1.9710(JD-1)     )
;3.648 sin( 1.9710(JD-1) + 90)]
;

5:  Z=X*F (P37)
 1: 52       X Loc [ JD_1      ]
 2: .98549   F
 3: 34       Z Loc [ t0        ]

6:  Z=X+F (P34)
 1: 34       X Loc [ t0        ]
 2: 90       F
 3: 35       Z Loc [ t1        ]

7:  Z=X*F (P37)
 1: 34       X Loc [ t0        ]
 2: 2        F
 3: 36       Z Loc [ t2        ]

8:  Z=X+F (P34)
 1: 36       X Loc [ t2        ]
 2: 90       F
 3: 37       Z Loc [ t3        ]

9:  Beginning of Loop (P87)
 1: 0000     Delay
 2: 4        Loop Count

10:  Z=SIN(X) (P48)
 1: 34    -- X Loc [ t0        ]
 2: 34    -- Z Loc [ t0        ]

11:  End (P95)

12:  Z=X*F (P37)
 1: 34       X Loc [ t0        ]
 2: 7.416    F
 3: 34       Z Loc [ t0        ]

13:  Z=X*F (P37)
 1: 35       X Loc [ t1        ]
 2: -.258    F
 3: 35       Z Loc [ t1        ]

14:  Z=X*F (P37)
 1: 36       X Loc [ t2        ]
 2: 9.228    F
 3: 36       Z Loc [ t2        ]

15:  Z=X*F (P37)
 1: 37       X Loc [ t3        ]
 2: 3.648    F
 3: 37       Z Loc [ t3        ]

16:  Spatial Average (P51)
 1: 4        Swath
 2: 34       First Loc [ t0        ]
 3: 39       Avg Loc [ EOT___deg ]
;
;sin(dec) = .39795 sin( .9856((JD-1) - 172) + 90)
;

17:  Z=X+F (P34)
 1: 52       X Loc [ JD_1      ]
 2: -172     F
 3: 34       Z Loc [ t0        ]

18:  Z=X*F (P37)
 1: 34       X Loc [ t0        ]
 2: .9856    F
 3: 34       Z Loc [ t0        ]

19:  Z=X+F (P34)
 1: 34       X Loc [ t0        ]
 2: 90       F
 3: 34       Z Loc [ t0        ]

20:  Z=SIN(X) (P48)
 1: 34       X Loc [ t0        ]
 2: 34       Z Loc [ t0        ]

21:  Z=X*F (P37)
 1: 34       X Loc [ t0        ]
 2: .39795   F
 3: 40       Z Loc [ sin_dec_  ]
;
;cos(dec) = sqrt[ 1 - sin^2(dec) ]
;since the magnitude of dec is always < 23.5 deg
;

22:  Z=X*Y (P36)
 1: 40       X Loc [ sin_dec_  ]
 2: 40       Y Loc [ sin_dec_  ]
 3: 34       Z Loc [ t0        ]

23:  Z=F (P30)
 1: 1        F
 2: 00       Exponent of 10
 3: 35       Z Loc [ t1        ]

24:  Z=X-Y (P35)
 1: 35       X Loc [ t1        ]
 2: 34       Y Loc [ t0        ]
 3: 35       Z Loc [ t1        ]

25:  Z=SQRT(X) (P39)
 1: 35       X Loc [ t1        ]
 2: 41       Z Loc [ cos_dec_  ]
;
;cos(ha) = sin(ha + 90)
;
;ha + 90 = 270 - TZ*15 - LST(deg) + EOT(deg) + long
;

26:  Z=X*F (P37)
 1: 51       X Loc [ Time_Zone ]
 2: -15      F
 3: 34       Z Loc [ t0        ]

27:  Z=X+F (P34)
 1: 34       X Loc [ t0        ]
 2: 270      F
 3: 34       Z Loc [ t0        ]

28:  Z=X-Y (P35)
 1: 34       X Loc [ t0        ]
 2: 53       Y Loc [ LST___deg ]
 3: 34       Z Loc [ t0        ]

29:  Z=X+Y (P33)
 1: 34       X Loc [ t0        ]
 2: 39       Y Loc [ EOT___deg ]
 3: 34       Z Loc [ t0        ]

30:  Z=X+Y (P33)
 1: 34       X Loc [ t0        ]
 2: 50       Y Loc [ Longitude ]
 3: 34       Z Loc [ t0        ]

31:  Z=SIN(X) (P48)
 1: 34       X Loc [ t0        ]
 2: 42       Z Loc [ cos_ha_   ]
;
;Cos(Zen) =   cos(lat) * cos(dec) * cos(ha)
;+ sin(lat) * sin(dec)
;

32:  Z=SIN(X) (P48)
 1: 49       X Loc [ Latitude  ]
 2: 34       Z Loc [ t0        ]

33:  Z=X*Y (P36)
 1: 34       X Loc [ t0        ]
 2: 40       Y Loc [ sin_dec_  ]
 3: 35       Z Loc [ t1        ]

34:  Z=X+F (P34)
 1: 49       X Loc [ Latitude  ]
 2: 90       F
 3: 36       Z Loc [ t2        ]

35:  Z=SIN(X) (P48)
 1: 36       X Loc [ t2        ]
 2: 36       Z Loc [ t2        ]

36:  Z=X*Y (P36)
 1: 36       X Loc [ t2        ]
 2: 41       Y Loc [ cos_dec_  ]
 3: 36       Z Loc [ t2        ]

37:  Z=X*Y (P36)
 1: 36       X Loc [ t2        ]
 2: 42       Y Loc [ cos_ha_   ]
 3: 37       Z Loc [ t3        ]

38:  Z=X+Y (P33)
 1: 37       X Loc [ t3        ]
 2: 35       Y Loc [ t1        ]
 3: 43       Z Loc [ cos_zen_  ]

39:  End (P95)

40:  Beginning of Subroutine (P85)
 1: 2        Subroutine 2
;
;This subroutine will pulse on the driver
;relay for the RSB motor and read back
;through C2 the status of the driver relay.
;The duration of the pulse on is about
;200 msec.  C2 is low during rotations.
;
;The entire rotation sequence is performed only if the
;battery voltage is greater than 10.5 V.  This helps
;protect the battery from being drained during periods
;of little or no insolation.
;
;Loop = 600 = 200ms
;Loop = 800 = 267ms (use for slow-starting RSPs)
;

41:  Set Port(s) (P20)
 1: 9999     C8,C7,C6,C5 Options
 2: 9980     C4,C3,C2,C1 Options

42:  Read Ports (P25)
 1: 2        Mask (0..255); Read C2
 2: 34       Loc [ t0        ]

43:  IF (X<=>F) (P89)
 1: 34       X Loc [ t0        ]
 2: 1        =
 3: 0.0000   F
 4: 30       Then Do

44:  Z=Z+1 (P32)
 1: 14       Z Loc [ Err_off   ]

45:  Z=X+F (P34)
 1: 16       X Loc [ Err_flag  ]
 2: 16       F
 3: 16       Z Loc [ Err_flag  ]

46:  End (P95)

47:  IF (X<=>F) (P89)
 1: 12       X Loc [ Vbat____V ]
 2: 3        >=
 3: 10.5     F
 4: 30       Then Do

; Pulse C1 for a maximum of 1000 counts.
; Tests C2 every 10 counts and stop pulsing if motor is off tab
; Increment Error on if #_pulses reach 100 (meaning 1000 counts)

48:  Set Port(s) (P20)
 1: 9999     C8,C7,C6,C5 Options
 2: 9981     C4,C3,C2,C1 Options

49:  Z=F (P30)
 1: 0.0      F
 2: 00       Exponent of 10
 3: 17       Z Loc [ _pulses   ]

; Loop for a minimum of 500 counts
50:  Beginning of Loop (P87)
 1: 0000     Delay
 2: 500      Loop Count

51:  End (P95)

;Test loop sequence for max of 500 more loops
52:  Beginning of Loop (P87)
 1: 0000     Delay
 2: 50       Loop Count

53:  Beginning of Loop (P87)
 1: 0000     Delay
 2: 10       Loop Count

54:  End (P95)

55:  Z=Z+1 (P32)
 1: 17       Z Loc [ _pulses   ]

56:  If Flag/Port (P91)
 1: 52       Do if Port 2 is Low
 2: 31       Exit Loop if True

57:  End (P95) ;Pulse loops

; Increment Error on if #_pulses reach maximum
58:  IF (X<=>F) (P89)
 1: 17       X Loc [ _pulses   ]
 2: 3        >=
 3: 50       F
 4: 30       Then Do

59:  Z=Z+1 (P32)
 1: 13       Z Loc [ Err_on    ]

60:  Z=X+F (P34)
 1: 16       X Loc [ Err_flag  ]
 2: 256      F
 3: 16       Z Loc [ Err_flag  ]

61:  End (P95) ; Max #_pulses

62:  End (P95) ; Vbat> 10.5

63:  Set Port(s) (P20)
 1: 9999     C8,C7,C6,C5 Options
 2: 9980     C4..C1 = nc/nc/input/0

64:  End (P95)

End Program












-Input Locations-
1 GH___W_m2 3 3 1
2 DN___W_m2 3 1 1
3 DH___W_m2 3 2 1
4 AT______F 3 1 1
5 _________ 3 1 0
6 _________ 2 0 0
7 _________ 2 0 0
8 _________ 2 0 0
9 _________ 2 0 0
10 _________ 2 0 0
11 _________ 2 0 0
12 Vbat____V 3 3 1
13 Err_on    3 0 1
14 Err_off   3 0 1
15 Err_bd    3 0 1
16 Err_flag  3 5 5
17 _pulses   3 1 2
18 Revision  3 1 1
19 _________ 2 0 0
20 _________ 2 0 0
21 _________ 2 0 0
22 _________ 2 0 0
23 Well_wdth 3 4 1
24 x1__val   3 0 1
25 x1__ind   3 3 2
26 x2__val   3 0 1
27 x2__ind   3 3 2
28 min_val   3 1 1
29 min_ind   3 2 1
30 cen_val   3 1 0
31 cen_ind   3 3 1
32 s1__val   3 1 0
33 s2__val   3 1 0
34 t0        3 29 27
35 t1        3 13 17
36 t2        3 13 12
37 t3        3 5 5
38 C         3 4 4
39 EOT___deg 3 1 1
40 sin_dec_  3 3 1
41 cos_dec_  3 1 1
42 cos_ha_   3 1 1
43 cos_zen_  3 2 1
44 _________ 2 0 0
45 GH_____CF 3 4 1
46 _________ 2 0 0
47 _________ 2 0 0
48 _________ 2 0 0
49 Latitude  3 2 1
50 Longitude 3 1 1
51 Time_Zone 3 1 1
52 JD_1      3 4 3
53 LST___deg 3 1 1
54 _________ 2 0 0
55 _________ 2 0 0
56 GH_____mV 3 3 1
57 DN_____mV 3 1 2
58 DH_____mV 3 2 2
59 _________ 2 0 0
60 _________ 2 0 0
61 _________ 2 0 0
62 _________ 2 0 0
63 _________ 2 0 0
65 GHrn_W_m2 3 0 1
66 GH_rot_mV 3 1 1
67 _GH__W_m2 3 7 2
68 _DN__W_m2 3 3 2
69 _DH__W_m2 3 6 3
70 _AT_____F 3 1 0
71 _________ 2 0 0
72 _________ 2 0 0
73 _________ 2 0 0
74 _________ 2 0 0
75 _________ 2 0 0
76 _________ 2 0 0
77 _________ 2 0 0
78 _________ 2 0 0
79 _________ 2 0 0
80 _________ 2 0 0
81 _________ 2 0 0
82 _________ 2 0 0
83 _________ 2 0 0
84 _________ 2 0 0
85 _________ 2 0 0
86 _________ 2 0 0
87 _________ 2 0 0
88 _________ 2 0 0
89 _________ 2 0 0
90 _________ 2 0 0
91 _________ 2 0 0
92 _________ 2 0 0
93 _________ 2 0 0
94 _________ 2 0 0
95 _________ 2 0 0
96 _________ 2 0 0
97 _________ 2 0 0
98 _________ 2 0 0
99 _________ 2 0 0
100 Well_Arr  3 1 0
101 _________ 3 1 0
102 _________ 2 0 0
103 _________ 2 0 0
104 _________ 2 0 0
105 _________ 2 0 0
106 _________ 2 0 0
107 _________ 2 0 0
108 _________ 2 0 0
109 _________ 2 0 0
110 _________ 2 0 0
111 _________ 2 0 0
112 _________ 2 0 0
113 _________ 2 0 0
114 _________ 2 0 0
115 _________ 2 0 0
116 _________ 2 0 0
117 _________ 2 0 0
118 _________ 2 0 0
119 _________ 2 0 0
120 _________ 2 0 0
121 _________ 2 0 0
122 _________ 2 0 0
123 _________ 2 0 0
124 _________ 2 0 0
125 Well_Min  2 0 0
126 _________ 2 0 0
127 _________ 2 0 0
128 _________ 2 0 0
129 _________ 2 0 0
130 _________ 2 0 0
131 _________ 2 0 0
132 _________ 2 0 0
133 _________ 2 0 0
134 _________ 2 0 0
135 _________ 2 0 0
136 _________ 2 0 0
137 _________ 2 0 0
138 _________ 2 0 0
139 _________ 2 0 0
140 _________ 2 0 0
141 _________ 2 0 0
142 _________ 2 0 0
143 _________ 2 0 0
144 _________ 2 0 0
145 _________ 2 0 0
146 _________ 2 0 0
147 _________ 2 0 0
148 _________ 2 0 0
149 _________ 2 0 0
150 Delta_Arr 3 2 1
151 _________ 3 2 0
152 _________ 3 2 0
153 _________ 3 2 0
154 _________ 3 2 0
155 _________ 3 2 0
156 _________ 3 2 0
157 _________ 3 2 0
158 _________ 3 2 0
159 _________ 3 2 0
160 _________ 3 2 0
161 _________ 3 2 0
162 _________ 3 2 0
163 _________ 3 2 0
164 _________ 3 2 0
165 _________ 3 2 0
166 _________ 3 2 0
167 _________ 3 2 0
168 _________ 3 2 0
169 _________ 3 2 0
170 _________ 3 2 0
171 _________ 3 2 0
172 _________ 3 2 0
173 _________ 3 2 0
174 _________ 3 2 0
175 _________ 3 2 0
176 _________ 3 2 0
177 _________ 3 2 0
178 _________ 3 2 0
179 _________ 3 2 0
180 _________ 3 2 0
181 _________ 3 2 0
182 _________ 3 2 0
183 _________ 3 2 0
184 _________ 3 2 0
185 _________ 3 2 0
186 _________ 3 2 0
187 _________ 3 2 0
188 _________ 3 2 0
189 _________ 3 2 0
190 _________ 3 2 0
191 _________ 3 2 0
192 _________ 3 2 0
193 _________ 3 2 0
194 _________ 3 2 0
195 _________ 3 2 0
196 _________ 3 2 0
197 _________ 3 2 0
198 _________ 3 2 0
199 _________ 2 0 0
200 _________ 2 0 0
201 _________ 2 0 0
202 _________ 2 0 0
203 _________ 2 0 0
204 _________ 2 0 0
205 _________ 2 0 0
206 _________ 2 0 0
207 _________ 2 0 0
208 _________ 2 0 0
209 _________ 2 0 0
210 _________ 2 0 0
211 _________ 2 0 0
212 _________ 2 0 0
213 _________ 2 0 0
214 _________ 2 0 0
215 _________ 2 0 0
216 _________ 2 0 0
217 _________ 2 0 0
218 _________ 2 0 0
219 _________ 2 0 0
220 _________ 2 0 0
221 _________ 2 0 0
222 _________ 2 0 0
223 _________ 2 0 0
224 _________ 2 0 0
225 _________ 2 0 0
226 _________ 2 0 0
227 _________ 2 0 0
228 _________ 2 0 0
229 _________ 2 0 0
230 _________ 2 0 0
231 _________ 2 0 0
232 _________ 2 0 0
233 _________ 2 0 0
234 _________ 2 0 0
235 _________ 2 0 0
236 _________ 2 0 0
237 _________ 2 0 0
238 _________ 2 0 0
239 _________ 2 0 0
240 _________ 2 0 0
241 _________ 2 0 0
242 _________ 2 0 0
243 _________ 2 0 0
244 _________ 2 0 0
245 _________ 2 0 0
246 _________ 2 0 0
247 _________ 2 0 0
248 _________ 2 0 0
249 _________ 2 0 0
250 Burst_Dat 3 3 1
275 Curve_Dat 3 1 0
925 GHII      3 2 0
926 _________ 0 0 0
927 _________ 0 0 0
928 _________ 0 0 0
929 _________ 0 0 0
930 _________ 0 0 0
931 _________ 0 0 0
932 _________ 0 0 0
933 _________ 0 0 0
934 _________ 0 0 0
935 _________ 0 0 0
936 _________ 0 0 0
937 _________ 0 0 0
938 _________ 0 0 0
939 _________ 0 0 0
940 _________ 0 0 0
941 _________ 0 0 0
942 _________ 0 0 0
943 _________ 0 0 0
944 _________ 0 0 0
945 _________ 0 0 0
946 _________ 0 0 0
947 _________ 0 0 0
948 _________ 0 0 0
949 _________ 0 0 0
950 _________ 0 0 0
-Program Security-
5555
5555
0
-Mode 4-
-Final Storage Area 2-
1920
-CR10X ID-
0
-CR10X Power Up-
3
