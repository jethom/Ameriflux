;{CR23X}
;WILLOW CREEK
;
;10 Hz SAMPLING
*Table 1 Program
  01: .1        Execution Interval (seconds) ;

;CSAT 3D SONIC ANEMOMETER
1:  SDM-CSAT3 (P107)
 1: 1        Reps
 2: 3        SDM Address
 3: 91       Trigger and Get wind & Ts data
 4: 1        Ux Input Location [ Ux        ]

2:  If (X<=>F) (P89)
 1: 5        X Loc [ CSAT_Diag ]
 2: 1        =
 3: 61440    F
 4: 21       Set Flag 1 Low

3:  If Flag/Port (P91)
 1: 21       Do if Flag 1 is Low
 2: 30       Then Do

4:  If (X<=>F) (P89)
 1: 5        X Loc [ CSAT_Diag ]
 2: 3        >=
 3: 0        F
 4: 30       Then Do

5:  If (X<=>F) (P89)
 1: 5        X Loc [ CSAT_Diag ]
 2: 2        <>
 3: 61503    F
 4: 30       Then Do

6:  Do (P86)
 1: 11       Set Flag 1 High

7:  SDM-CSAT3 (P107)
 1: 1        Reps
 2: 3        SDM Address
 3: 10       Set Execution Parameter
 4: 1        Ux Input Location [ Ux        ]

8:  End (P95)

9:  End (P95)

10:  End (P95)

;LI-COR 6262
11:  Volt (SE) (P1)
 1: 4        Reps
 2: 25       5000 mV, 60 Hz Reject, Fast Range
 3: 9        SE Channel
 4: 12       Loc [ CO2       ]
 5: 1.0      Mult
 6: 0.0      Offset ;

12:  Z=X*F (P37)
 1: 14       X Loc [ LI_T      ]
 2: .012207  F
 3: 14       Z Loc [ LI_T      ] ; deg C

13:  Polynomial (P55)
 1: 1        Reps
 2: 15       X Loc [ LI_P      ]
 3: 15       F(X) Loc [ LI_P      ] ; kPa
 4: 58.738   C0
 5: .01522   C1
 6: 0.0      C2
 7: 0.0      C3
 8: 0.0      C4
 9: 0.0      C5

14:  Do (P86)
 1: 10       Set Output Flag High (Flag 0)

*Table 2 Program
  01: .1        Execution Interval (seconds)

1:  If time is (P92)
 1: 0     -- Minutes (Seconds --) into a
 2: 5     -- Interval (same units as above)
 3: 30       Then Do

;KIPP & ZONEN
2:  Volt (Diff) (P2)
 1: 4        Reps
 2: 22       50 mV, 60 Hz Reject, Slow Range
 3: 7        DIFF Channel
 4: 38       Loc [ Solar_1   ]
 5: 91.659   Mult
 6: 0.0      Offset

3:  Full Bridge w/mv Excit (P9)
 1: 1        Reps
 2: 24       1000 mV, 60 Hz Reject, Slow, Ex Range
 3: 22       50 mV, 60 Hz Reject, Slow, Br Range
 4: 2        DIFF Channel
 5: 3        Excite all reps w/Exchan 3
 6: 500      mV Excitation
 7: 43       Loc [ Rs_Ro     ]
 8: 100      Mult
 9: 0.0      Offset

4:  Temperature RTD (P16)
 1: 1        Reps
 2: 43       R/R0 Loc [ Rs_Ro     ]
 3: 42       Loc [ KZ_T      ]
 4: 1.0      Mult
 5: 0.0      Offset

5:  End (P95)

6:  If time is (P92)
 1: 1     -- Minutes (Seconds --) into a
 2: 5     -- Interval (same units as above)
 3: 30       Then Do

;CS105 BAROMETRIC PRESSURE SENSOR
7:  Volt (SE) (P1)
 1: 1        Reps
 2: 25       5000 mV, 60 Hz Reject, Fast Range
 3: 1        SE Channel
 4: 6        Loc [ Atm_P     ]
 5: .184     Mult
 6: 661.065  Offset

8:  End (P95)

9:  Z=X*F (P37) ;mb to kPa conversion
 1: 6        X Loc [ Atm_P     ]
 2: 0.1      F
 3: 6        Z Loc [ Atm_P     ]

10:  If time is (P92)
 1: 2     -- Minutes (Seconds --) into a
 2: 5     -- Interval (same units as above)
 3: 30       Then Do

;CHILLED MIRROR HYGROMETER
11:  Volt (Diff) (P2)
 1: 1        Reps
 2: 25       5000 mV, 60 Hz Reject, Fast Range
 3: 4        DIFF Channel
 4: 10       Loc [ Dew_Pt    ]
 5: .01      Mult
 6: 0        Offset

12:  Z=X+F (P34)
 1: 10       X Loc [ Dew_Pt    ]
 2: -10.4    F
 3: 10       Z Loc [ Dew_Pt    ]

13:  Z=X*F (P37)
 1: 10       X Loc [ Dew_Pt    ]
 2: 6.25     F
 3: 10       Z Loc [ Dew_Pt    ]

14:  End (P95)

15:  If time is (P92)
 1: 3     -- Minutes (Seconds --) into a
 2: 5     -- Interval (same units as above)
 3: 30       Then Do

;RTD AIR TEMPERATURE PROBE
16:  Full Bridge w/mv Excit (P9)
 1: 1        Reps
 2: 24       1000 mV, 60 Hz Reject, Slow, Ex Range
 3: 24       1000 mV, 60 Hz Reject, Slow, Br Range
 4: 2        DIFF Channel
 5: 3        Excite all reps w/Exchan 3
 6: 2500     mV Excitation
 7: 8        Loc [ RTD_T     ]
 8: 1.0      Mult
 9: 0.0      Offset

17:  Polynomial (P55)
 1: 1        Reps
 2: 8        X Loc [ RTD_T     ]
 3: 8        F(X) Loc [ RTD_T     ]
 4: -250.18  C0
 5: 238.66   C1
 6: 11.628   C2
 7: 0.0      C3
 8: 0.0      C4
 9: 0.0      C5

18:  Z=X+F (P34)
 1: 8        X Loc [ RTD_T     ]
 2: -.0283   F
 3: 8        Z Loc [ RTD_T     ]

19:  End (P95)

20:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 1        Interval (same units as above)
 3: 30       Then Do

;BATTERY VOLTAGE
21:  Batt Voltage (P10)
 1: 9        Loc [ Battery   ] ;

;237 LEAF WETNESS SENSOR
22:  AC Half Bridge (P5)
 1: 1        Reps
 2: 13       200 mV, Fast Range
 3: 2        SE Channel
 4: 2        Excite all reps w/Exchan 2
 5: 2500     mV Excitation
 6: 7        Loc [ Leaf_Wet  ]
 7: 1.0      Mult
 8: 0.0      Offset

23:  Z=1/X (P42)
 1: 7        X Loc [ Leaf_Wet  ]
 2: 7        Z Loc [ Leaf_Wet  ]

24:  Z=X+F (P34)
 1: 7        X Loc [ Leaf_Wet  ]
 2: -101.0   F
 3: 7        Z Loc [ Leaf_Wet  ]

25:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 1        Interval (same units as above)
 3: 10       Set Output Flag High (Flag 0)

26:  Set Active Storage Area (P80)
 1: 02       Final Storage Area 2
 2: 2        Array ID

27:  Real Time (P77)
 1: 1111     Year,Day,Hour/Minute,Seconds (midnight = 0000)

28:  Average (P71)
 1: 1        Reps
 2: 6        Loc [ Atm_P     ]

29:  Histogram (P75)
 1: 1        Reps
 2: 1        No. of Bins
 3: 1        Closed Form
 4: 7        Bin Select Value Loc [ Leaf_Wet  ]
 5: 11       WV Loc Option [ _________ ]
 6: 0.0      Low Limit
 7: 150      High Limit

*Table 3 Subroutines

End Program

-Input Locations-
1 Ux        5 0 2
2 Uy        9 0 2
3 Uz        9 0 2
4 Ts        9 0 2
5 CSAT_Diag 17 3 2
6 Atm_P     1 2 2
7 Leaf_Wet  1 3 3
8 RTD_T     1 2 3
9 Battery   1 0 1
10 Dew_Pt    1 2 3
11 _________ 1 1 0
12 CO2       5 0 1
13 H20       9 0 1
14 LI_T      9 1 2
15 LI_P      17 1 2
16 Panel_T   1 0 0
17 CSI_R     0 0 0
18 CSI_1     0 0 0
19 CSI_2     0 0 0
20 V_P0_P    0 0 0
21 CSI_3     0 0 0
22 CSI_4     0 0 0
23 FV        0 0 0
24 CO2_PPM   0 0 0
25 EA        0 0 0
26 RTD_VT    0 0 0
27 _________ 0 0 0
28 MIX_RATIO 0 0 0
29 RH        0 0 0
30 ES2       0 0 0
31 ES        0 0 0
32 H2O       0 0 0
33 HV_P0_P   0 0 0
34 HFV       0 0 0
35 H20_GKG   0 0 0
36 CV_P0_P   0 0 0
37 CFV       0 0 0
38 Solar_1   5 0 1
39 Solar_2   9 0 1
40 Solar_3   9 0 1
41 Solar_4   17 0 1
42 KZ_T      1 0 1
43 Rs_Ro     1 1 1
-Program Security-
0
0
0
-Mode 4-
-Final Storage Area 2-
500000
-CR10X ID-
0
-CR10X Power Up-
3
