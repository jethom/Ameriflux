;{CR23X}
;WILLOW CREEK
;
;10 Hz SAMPLING
*Table 1 Program
  01: .1        Execution Interval (seconds) ;

;CSAT 3D SONIC ANEMOMETER
1:  SDM-CSAT3 (P107)
 1: 1        Reps
 2: 3        SDM Address
 3: 91       Trigger and Get wind & Ts data
 4: 1        Ux Input Location [ Ux        ]

2:  If (X<=>F) (P89)
 1: 5        X Loc [ CSAT_Diag ]
 2: 1        =
 3: 61440    F
 4: 21       Set Flag 1 Low

3:  If Flag/Port (P91)
 1: 21       Do if Flag 1 is Low
 2: 30       Then Do

4:  If (X<=>F) (P89)
 1: 5        X Loc [ CSAT_Diag ]
 2: 3        >=
 3: 0        F
 4: 30       Then Do

5:  If (X<=>F) (P89)
 1: 5        X Loc [ CSAT_Diag ]
 2: 2        <>
 3: 61503    F
 4: 30       Then Do

6:  Do (P86)
 1: 11       Set Flag 1 High

7:  SDM-CSAT3 (P107)
 1: 1        Reps
 2: 3        SDM Address
 3: 10       Set Execution Parameter
 4: 1        Ux Input Location [ Ux        ]

8:  End (P95)

9:  End (P95)

10:  End (P95)

;LI-COR 6262
11:  Volt (SE) (P1)
 1: 4        Reps
 2: 25       5000 mV, 60 Hz Reject, Fast Range
 3: 9        SE Channel
 4: 12       Loc [ CO2       ]
 5: 1.0      Mult
 6: 0.0      Offset ;

12:  Z=X*F (P37)
 1: 14       X Loc [ LI_T      ]
 2: .012207  F
 3: 14       Z Loc [ LI_T      ] ; deg C

13:  Polynomial (P55)
 1: 1        Reps
 2: 15       X Loc [ LI_P      ]
 3: 15       F(X) Loc [ LI_P      ] ; kPa
 4: 58.738   C0
 5: .01522   C1
 6: 0.0      C2
 7: 0.0      C3
 8: 0.0      C4
 9: 0.0      C5

;H2O APPROXIMATION
HV_P0_P=H2O*(101.3/LI_P)
HFv=(6.7305E-03*HV_P0_P)+(2.8382E-06*HV_P0_P^2)+(3.0464E-11*HV_P0_P^3)
H20_gkg=HFv*((LI_T+273)/(40.9+273.16))

;CO2 APPROXIMATION
CV_P0_P=CO2*(101.3/LI_P)
CFv=(.14312*CV_P0_P)+(1.0651E-05*CV_P0_P^2)+(7.0238E-09*CV_P0_P^3)+(-9.4371E-13*CV_P0_P^4)+(6.5383E-17*CV_P0_P^5)
CO2_ppm=CFv*((LI_T+273.16)/(34.6+273.16))

14:  Do (P86)
 1: 10       Set Output Flag High (Flag 0)

*Table 2 Program
  01: 1         Execution Interval (seconds)

1:  If time is (P92)
 1: 0000  -- Minutes (Seconds --) into a
 2: 5        Interval (same units as above)
 3: 30       Then Do

;CS105 BAROMETRIC PRESSURE SENSOR
2:  Volt (SE) (P1)
 1: 1        Reps
 2: 25       5000 mV, 60 Hz Reject, Fast Range
 3: 1        SE Channel
 4: 6        Loc [ Atm_P     ]
 5: .184     Mult
 6: 661.065  Offset

3:  Z=X*F (P37) ;mb to kPa conversion
 1: 6        X Loc [ Atm_P     ]
 2: 0.1      F
 3: 6        Z Loc [ Atm_P     ]

;237 LEAF WETNESS SENSOR
4:  AC Half Bridge (P5)
 1: 1        Reps
 2: 13       200 mV, Fast Range
 3: 2        SE Channel
 4: 2        Excite all reps w/Exchan 2
 5: 2500     mV Excitation
 6: 7        Loc [ Leaf_Wet  ]
 7: 1.0      Mult
 8: 0.0      Offset

5:  Z=1/X (P42)
 1: 7        X Loc [ Leaf_Wet  ]
 2: 7        Z Loc [ Leaf_Wet  ]

6:  Z=X+F (P34)
 1: 7        X Loc [ Leaf_Wet  ]
 2: -101.0   F
 3: 7        Z Loc [ Leaf_Wet  ]

;CHILLED MIRROR HYGROMETER
7:  Volt (Diff) (P2)
 1: 1        Reps
 2: 25       5000 mV, 60 Hz Reject, Fast Range
 3: 4        DIFF Channel
 4: 10       Loc [ Dew_Pt    ]
 5: .01      Mult
 6: 0        Offset

8:  Z=X+F (P34)
 1: 10       X Loc [ Dew_Pt    ]
 2: -10.4    F
 3: 10       Z Loc [ Dew_Pt    ]

9:  Z=X*F (P37)
 1: 10       X Loc [ Dew_Pt    ]
 2: 6.25     F
 3: 10       Z Loc [ Dew_Pt    ]

;MULTPLEXER
10:  Do (P86)
 1: 44       Set Port 4 High

11:  Beginning of Loop (P87)
 1: 0000     Delay
 2: 1        Loop Count

12:  Do (P86)
 1: 75       Pulse Port 5

13:  Delay w/Opt Excitation (P22)
 1: 1        Ex Channel
 2: 0000     Delay W/Ex (units = 0.01 sec)
 3: 1        Delay After Ex (units = 0.01 sec)
 4: 0        mV Excitation

;RTD TEMPERATURE PROBE
14:  Full Bridge w/mv Excit (P9)
 1: 1        Reps
 2: 24       1000 mV, 60 Hz Reject, Slow, Ex Range
 3: 24       1000 mV, 60 Hz Reject, Slow, Br Range
 4: 2        DIFF Channel
 5: 3        Excite all reps w/Exchan 3
 6: 2500     mV Excitation
 7: 8        Loc [ RTD_T     ]
 8: 1.0      Mult
 9: 0.0      Offset

15:  Polynomial (P55)
 1: 1        Reps
 2: 8        X Loc [ RTD_T     ]
 3: 8        F(X) Loc [ RTD_T     ]
 4: -250.18  C0
 5: 238.66   C1
 6: 11.628   C2
 7: 0.0      C3
 8: 0.0      C4
 9: 0.0      C5

16:  Z=X+F (P34)
 1: 8        X Loc [ RTD_T     ]
 2: -.0283   F
 3: 8        Z Loc [ RTD_T     ]

17:  End (P95)

18:  Do (P86)
 1: 54       Set Port 4 Low

19:  End (P95)

20:  If time is (P92)
 1: 1     -- Minutes (Seconds --) into a
 2: 5        Interval (same units as above)
 3: 30       Then Do

;BATTERY VOLTAGE
21:  Batt Voltage (P10)
 1: 9        Loc [ Battery   ] ;

;CR23X PANEL TEMPERATURE
22:  Panel Temperature (P17)
 1: 16       Loc [ Panel_T   ]

;VIRTUAL T, RH,AND MIXING RATIO (g H2O/kg dry air) CALCULATIONS
ea=0.61083*10^((7.6448*Dew_Pt)/(242.62+Dew_Pt))
RTD_VT=RTD_T*(1+0.61*ea/(Atm_P-ea))
es=0.61083*10^((7.6448*RTD_VT)/(242.62+RTD_VT))
Mix_Ratio=ea/(Atm_P-ea)*10^3
RH=ea/es*100

23:  End (P95)

24:  If time is (P92)
 1: 0     -- Minutes (Seconds --) into a
 2: 1        Interval (same units as above)
 3: 10       Set Output Flag High (Flag 0)

25:  Set Active Storage Area (P80)
 1: 02       Final Storage Area 2
 2: 2        Array ID

26:  Real Time (P77)
 1: 1111     Year,Day,Hour/Minute,Seconds (midnight = 0000)

27:  Average (P71)
 1: 1        Reps
 2: 6        Loc [ Atm_P     ]

28:  Histogram (P75)
 1: 1        Reps
 2: 1        No. of Bins
 3: 1        Closed Form
 4: 7        Bin Select Value Loc [ Leaf_Wet  ]
 5: 11       WV Loc Option [ _________ ]
 6: 0.0      Low Limit
 7: 150      High Limit

*Table 3 Subroutines

End Program

-Input Locations-
1 Ux        5 0 2
2 Uy        9 0 2
3 Uz        9 0 2
4 Ts        9 0 2
5 CSAT_Diag 17 3 2
6 Atm_P     1 2 2
7 Leaf_Wet  1 3 3
8 RTD_T     1 2 3
9 Battery   1 0 1
10 Dew_Pt    1 2 3
11 _________ 1 1 0
12 CO2       5 0 1
13 H20       9 0 1
14 LI_T      9 1 2
15 LI_P      17 1 2
16 Panel_T   1 0 1
17 CSI_R     0 0 0
18 CSI_1     0 0 0
19 CSI_2     0 0 0
20 V_P0_P    0 0 0
21 CSI_3     0 0 0
22 CSI_4     0 0 0
23 FV        0 0 0
24 CO2_PPM   0 0 0
25 EA        0 0 0
26 RTD_VT    0 0 0
27 _________ 0 0 0
28 MIX_RATIO 0 0 0
29 RH        0 0 0
30 ES2       0 0 0
31 ES        0 0 0
-Program Security-
0
0
0
-Mode 4-
-Final Storage Area 2-
500000
-CR10X ID-
0
-CR10X Power Up-
3
